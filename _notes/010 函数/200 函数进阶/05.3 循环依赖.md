---
created: 2022-06-29
tags: 
- 上下文 
- calculate 
- 循环依赖 
- dax圣经
subject: dax圣经
importance: 5
skilled: 3
status:
author: sqlbi
url:
cover: 
---


在设计数据模型时，应该注意公式中循环依赖关系。先看下面的计算列:

```js
Sales[Margin] =
Sales[Net Price] - Sales[Unit Cost]
```

在本例中， 只有在 _Net Price_ 和 _Unit Cost_ 已经有一个值之后才能计算 _Margin_。

DAX 可以很好地处理依赖关系，但编写的代码也可能出现循环依赖的关系。当依赖关系链中存在循环，DAX 无法确定表达式的求值顺序时，就会发生循环依赖关系。

例如，考虑以下两个计算列:

```js
Sales[MarginPct] =
DIVIDE ( Sales[Margin], Sales[Unit Cost] )

Sales[Margin] =
Sales[MarginPct] * Sales[Unit Cost]
```

在这段代码中，_MarginPct_ 依赖于 _Margin，同时 Margin_ 也依赖于 _MarginPct_。依赖关系链中有一个循环。在这种情况下，DAX 拒绝接受最后一个公式，并显示错误，“检测到循环依赖关系”。

如果 A 依赖于 B，同时 B 就不能依赖于 A。 CALCULATE 的使用中可能会产生循环依赖。

假设 _Sales_ 中有一个计算列，其代码如下:

```js
Sales[AllSalesQty] =
CALCULATE ( SUM ( Sales[Quantity] ) )
```

因为 CALCULATE 在行上下文中运行，所以表中所有列的当前值都隐性地包含在表达式中。因此，DAX 计算的实际表达式如下：

```js
Sales[AllSalesQty] =
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[ProductKey] = <CurrentValueOfProductKey>,
    Sales[StoreKey] = <CurrentValueOfStoreKey>,
    ...,
    Sales[Margin] = <CurrentValueOfMargin>
)
```

如上所见，_AllSalesQty_ 所依赖的列实际上是表的完整列集。一旦在行上下文中使用了 _CALCULATE_，计算就会马上依赖于被迭代表的所有列。这在计算列中更为明显，因为行上下文会自动出现在计算列中。

如果使用 _CALCULATE_ 编写单个的计算列，那么代码的执行不会有问题。如果试图在一个表中创建两个的计算列，且**两个列都使用 **_**CALCULATE**_，那么在这两列都会出现上下文转换，问题就会出现。

```js
Sales[NewAllSalesQty] = CALCULATE ( SUM ( Sales[Quantity] ) )
```

![](https://secure2.wostatic.cn/static/ja5mUWh2A5csUZEVQtt3p8/image.png)

原因是 CALCULATE 将表的所有列添加为筛选参数，向表中添加新列也会更改已有列的定义。

如果能够创建 _NewAllSalesQty_，那么这两个计算列的代码将如下所示：

```js
Sales[AllSalesQty] =
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[ProductKey] = <CurrentValueOfProductKey>,
    ...,
    Sales[Margin] = <CurrentValueOfMargin>,
    Sales[NewAllSalesQty] = <CurrentValueOfNewAllSalesQty> --引用第 16 行
)

Sales[NewAllSalesQty] =
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[ProductKey] = <CurrentValueOfProductKey>,
    ...,
    Sales[Margin] = <CurrentValueOfMargin>,
    Sales[AllSalesQty] = <CurrentValueOfAllSalesQty> -- 引用第 7 行
)
```

代码第 7 行和第 16 行的相互引用。_AllSalesQty_ 依赖于 _NewAllSalesQty_ 的值，同时，_NewAllSalesQty_ 依赖于 _AllSalesQty_ 的值。尽管隐藏得很好，但确实存在循环依赖关系。DAX 检测到循环依赖关系，会拒绝接受这种代码。

如果 _CALCULATE_ 执行上下文转换的表包含一个具有唯一值的列，并且 DAX 知道这一点，那么从依赖关系的角度，上下文转换仅筛选该列。

例如，考虑 _Product_ 表中使用以下代码计算的列:

```js
'Product'[ProductSales] =
CALCULATE ( SUM ( Sales[Quantity] ) )
```

在本例中，不需要将所有列添加为筛选参数。事实上，_Product_ 包含一个 _ProductKey_ 列，它在 _Product_ 表的每一行都有一个唯一值。DAX 引擎知道这一点，因为该列位于“一对多”关系中的“一”侧。因此，当上下文转换发生时，引擎知道向每个列添加筛选器是没有意义的。代码被引擎翻译如下:

```js
'Product'[ProductSales] =
CALCULATE (
   SUM ( Sales[Quantity] ),
   'Product'[ProductKey] = <CurrentValueOfProductKey>
)
```

可以看到，_Product_ 表中的 _ProductSales_ 计算列完全依赖于 _ProductKey。因此，可以使用 CALCULATE_ 创建许多计算列，因为它们都只依赖于具有唯一值的列。

上面关于上下文转换的最后一句话并不完全准确。我们只是把它用于教学目的。

-   _CALCULATE_ 将表的所有列添加为筛选参数，即使存在行标识符。不过，内部依赖关系仅在唯一列上创建。唯一列的存在使得 DAX 可以用 _CALCULATE_ 计算多个列。
-   无论有没有唯一列，_CALCULATE_ 的语义都是相同的:添加被迭代表的所有列为筛选参数。

我们已经讨论了这样一个事实：对包含重复行的表进行上下文转换是一个严重的问题。循环依赖关系的存在是另一个很好的理由，当不能保证行的唯一性时，应该避免使用 _CALCULATE_ 和上下文转换。

包含唯一值的列不足以确保 _CALCULATE_ 会依赖于它进行上下文转换。必须让数据模型知道到这一列。DAX 如何知道某列包含唯一值?有多种方法可以向引擎提供这些信息:

-   当表是关系的主键列（“一对多”的“一”端）时，用于构建关系的列被标记为唯一的。这适用于任何工具。
-   当某列被设置为“Mark as Date Table（标记为日期表）”时，该列是隐式唯一的——更多信息见第8章“时间智能计算”。
-   通过“_Table Behavior_（表行为）_”_手动设置唯一列的行标识符。此方法仅适用于 Excel 和 Analysis Services 表格中的 Power Pivot；在编写本文时，Power BI 中还不支持这种方法。

这些操作中的任何一个都会通知 DAX 引擎表有一个行标识符，同时会停止不遵守行标识符约束的表格处理。当表具有行标识符时，可以使用 _CALCULATE_ 而不用担心循环依赖关系。原因是上下文转换只依赖于键列。

> 虽然被描述为一个特性，但这种行为实际上是优化的副作用。DAX 的语义要求依赖所有列。在引擎中很早就引入的特定优化只会创建对表的主键的依赖。因为现在许多用户都依赖这种行为，它已经成为语言的一部分。不过，它仍然是一个优化。在边缘场景中——例如将_USERELATIONSHIP_作为公式的一部分使用时——优化不会起作用，从而导致循环依赖关系错误。

相关阅读：l
```dataview
list 
where contains(tags,"循环依赖")
and file.name != this.file.name
```