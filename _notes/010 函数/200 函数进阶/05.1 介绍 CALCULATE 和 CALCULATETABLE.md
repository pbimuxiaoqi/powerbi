---
created: 2022-06-29
tags: 
- 上下文 
- calculate 
- dax圣经
subject: dax圣经
importance: 5
skilled: 3
status:
author: sqlbi
url:
cover: 
---


## 创建筛选上下文

首先需要做以下计算：

```js
Sales Amount := // 销售额 = 销售数量 * 销售净价格
SUMX ( Sales, Sales[Quantity] * Sales[Net Price] )

Gross Margin := // 毛利 = 销售数量 *（ 销售净价格 - 单位成本 ）
SUMX ( Sales, Sales[Quantity] * ( Sales[Net Price] - Sales[Unit Cost] ) ) 

GM % := // 毛利率 = 毛利 / 销售额
DIVIDE ( [Gross Margin], [Sales Amount] )
```

![[Pasted image 20220629211904.png]]

显示了另外两列：Contoso 品牌产品的毛利，包括金额和百分比。

![[Pasted image 20220629211911.png]]

用 [[FILTER]] 将计算范围限制在 Contoso 产品上。

```js
Contoso GM :=
VAR ContosoSales =      -- 保存与 Contoso 产品
    FILTER (            -- 相关的销售行到变量
        Sales,
        RELATED ( 'Product'[Brand] ) = "Contoso"
    )
VAR ContosoMargin =     -- 通过迭代 ContosoSales 表
    SUMX (              -- 只计算 Contoso 产品的利润
        ContosoSales,
        Sales[Quantity] * ( Sales[Net Price] - Sales[Unit Cost] )
    )
RETURN
    ContosoMargin
```

_ContosoSales_ 变量包含 _Sales_ 表中所有与 Contoso 品牌产品相关的行。一旦计算了变量，SUMX 就会在 _ContosoSales_ 迭代计算利润。因为实际是在 _Sales_ 表迭代，在 _Product_ 表筛选，所以使用 [[RELATED]] 检索 _Sales_ 表中每一行的相关产品。同样，我们可以通过两次迭代 _ContosoSales_ 变量来计算 Contoso 的毛利率：

```js
Contoso GM % :=
VAR ContosoSales =            -- 筛选 Sales 表中 Contoso 品牌产品
    FILTER (                  
        Sales,
        RELATED ( 'Product'[Brand] ) = "Contoso"
    )
VAR ContosoMargin =            -- 迭代 ContosoSales
    SUMX (                     -- 计算 Contoso 的利润
        ContosoSales,
        Sales[Quantity] * ( Sales[Net Price] - Sales[Unit Cost] )
    )
VAR ContosoSalesAmount =       -- 迭代 ContosoSales
    SUMX (                     -- 计算 Contoso 的销售额
        ContosoSales,
        Sales[Quantity] * Sales[Net Price]
    )
VAR Ratio =
    DIVIDE ( ContosoMargin, ContosoSalesAmount )
RETURN
    Ratio
```

_Contoso GM %的代码稍长，从逻辑上来看，与 Contoso GM_ 的模式相同。事实上，该模型已经包含计算毛利和毛利率的度量值，但是因为新的度量值需要增加筛选条件，我们不得不重新写表达式。

需要强调的是，度量值 _Gross Margin_ 和 _GM %已经可以计算出与 Contoso 相关的值。实际上从图 5-2 中可以看出，Contoso 的毛利为 3,877,070.65，毛利率为 52.73%。用 Brand_ 对 _Gross Margin_ 和 _GM %_度量值切片，可得到相同的数字，如图 5-3 所示。

![[Pasted image 20220629211921.png]]

**图** **5-3** 基础度量值按品牌切片，计算 Contoso 的 Gross Margin 和 GM %

在红框选中的单元格中，报告创建的筛选上下文筛选了 Contoso 品牌。筛选上下文筛选模型。因为 _Sales_ 与 _Product_ 表之间存在关联，所以 _Product[Brand]列上的筛选上下文会筛选 Sales_ 表。因为筛选上下文作用于整个模型，所以使用筛选上下文可间接筛选某个表。

因此，如果我们通过编程方式创建只筛选 Contoso 产品的筛选上下文之后，让 DAX 计算 _Gross Margin_ 度量值，那么 _Contoso GM_ 和 _Contoso GM %_两个度量值的的实现会容易得多。这可以通过使用 CALCULATE 实现。

首先我们来看 CALCULATE 的语法：

```js
CALCULATE ( Expression, Condition1, … ,ConditionN )
```

CALCULATE 可以接受任意数量的参数。唯一的强制参数是第一个参数，即要计算的表达式。第一个参数后面的条件称为筛选参数。[[CALCULATE]] 基于一组筛选参数创建新的筛选上下文。在创建新的筛选上下文后，CALCULATE 将其应用于模型后，计算第一个参数中的表达式。因此通过使用 CALCULATE，C_ontoso Margin_ 和 _Contoso GM %_的代码变得简单得多：

```js
Contoso GM :=
CALCULATE ( [Gross Margin],        -- 计算毛利
    'Product'[Brand] = "Contoso" ) -- 筛选上下文 brand = Contoso

Contoso GM% :=
CALCULATE ( [GM %],                -- 计算毛利率
    'Product'[Brand] = "Contoso" ) -- 筛选上下文 brand = Contoso
```


<font color="red">CALCULATE允许您通过在当前上下文中操作筛选器来创建新的筛选上下文</font>

## 介绍 CALCULATE

**只有 CALCULATE和 CALCULATETABLE 是可以修改筛选上下文的DAX函数**。它们不修改现有筛选上下文；而是通过将筛选参数与现有筛选上下文合并来创建一个新的筛选上下文。一旦含有它们的公式运行结束，公式形成的新的筛选上下文失效，之前的筛选上下文再次生效。我们仍用 CALCULATE 的示例来说明。

我们已经介绍了 [[CALCULATE]] 的语法是：

```js
CALCULATE ( Expression, Condition1, … ,ConditionN )
```

-   第一个参数是 将要计算的表达式。在计算表达式之前，先计算筛选参数并将其应用于筛选上下文，且多个筛选器并行执行。

关于 _CALCULATE，需要注意的第一件重要事情是：**筛选参数不是布尔条件，而是表。当您使用布尔条件作为 CALCULATE_ 的筛选参数时，DAX 将其转换为一个由筛选值组成的表。**

在上一节中，我们使用了以下代码：

```js
Contoso GM :=
CALCULATE ( [Gross Margin],        -- 计算毛利
    'Product'[Brand] = "Contoso" ) -- 筛选上下文 brand = Contoso
```

对于完整的 _CALCULATE_ 语法，使用布尔条件只是一种快捷方式。这就是所谓的**语法糖**。

> 语法糖，是一种对语法的简化，而其背后执行逻辑要考虑多种情况，而导致完整的写法是：

```js
Contoso GM :=
CALCULATE (
    [Gross Margin],                  -- 计算毛利
    FILTER (                         -- 筛选 Product[Brand]的有效值
         ALL ( 'Product'[Brand] ),   -- 任意 Product[Brand]的值
        'Product'[Brand] = "Contoso" -- 产品名称等于 "Contoso"

    )
)
```

这两种语法是等价的，它们之间没有性能或语义上的差异。但是，将 CALCULATE 筛选参数作为表来读取会使得 CALCULATE 的行为更加明显，对初学者更有帮助。

**筛选参数是一个表，即值列表。此表定义了指定列上由可见值组成的列表并用于表达式的计算**。在前面的例子中，FILTER 返回只有一行的表，即包含 Product[Brand]等于“Contoso”的表。换句话说，CALCULATE 函数使“Contoso”是 Product[Brand]列的唯一可见值。因此，CALCULATE 筛选出的模型只包括 Contoso 品牌的产品。考虑下面两个度量值:

```js
Sales Amount :=
SUMX ( Sales, Sales[Quantity] * Sales[Net Price] )

Contoso Sales :=
CALCULATE (
    [Sales Amount],
    FILTER ( ALL ( 'Product'[Brand] ), 'Product'[Brand] = "Contoso" )
)
```

在 Contoso Sales 的 CALCULATE 中，[[FILTER]] 的筛选参数扫描 [[ALL]](Product[Brand])。因此，原来在产品品牌列上的任何筛器选都会被新的筛选器覆盖。在图 5-4 中，您可以看到 _Contoso Sales_ 对所有行（品牌）报告的值都与 Contoso 的 _Sales Amount_ 相同。

![[Pasted image 20220629211934.png]]

**图** **5-4** _Contoso Sales_ 用 Contoso 筛选器覆盖现有筛选器

在每一行，报告都创建了一个包含相关品牌的筛选上下文。例如在 Litware 行中，报告创建的原始筛选上下文包含一个只显示 Litware 的筛选器。然后 CALCULATE 计算它的筛选参数并返回一个只包含 Contoso 的表。这个新创建的筛选器覆盖了同一列上的原始筛选器。在图 5-5 中可以看到整个流程图。

![[Pasted image 20220629211941.png]]

**图** **5-5** _CALCULATE_ 在计算时，筛选器 Litware 被筛选器 Contoso 覆盖

**[[CALCULATE]] 不会覆盖所有的原始筛选上下文，而是只替换与筛选参数同处一列的原始筛选器**。事实上，如果将报告更改为按 Product[Category]切片，结果会有所不同，如图 5-6 所示。

![[Pasted image 20220629211950.png]]

**图** **5-6** 如果报告被 Category 筛选，Brand 上的筛选器将被合并，而不会被覆盖

在上图中，报告筛选 *Product[Category]，而 CALCULATE 则应用 _Product[Brand]的筛选器来计算 Contoso Sales_ 度量值。这两个筛选器位于 _Product_ 的不同列上，所以不会发生覆盖，相反这两个筛选器会合并为一个新的筛选上下文发挥作用。因此，每个单元格都显示了 Contoso 品牌给定类别产品的销售额。具体过程如图 5-7 所示。

![[Pasted image 20220629211959.png]]

**图** **5-7** _CALCULATE_ 筛选参数会覆盖同一列上的筛选器，并对不同列上的筛选器进行合并

CALCULATE 总结如下：
-   **CALCULATE复制当前筛选上下文。**
-   CALCULATE计算每个筛选参数，并为每个条件生成指定列的有效值列表。
-   **如果两个或多个筛选参数作用于同一列，则使用  运算符(或数学术语中的集合交集)将它们合并在一起。**
-  **CALCULATE 使用新的条件替换模型中列上现有筛选器**。如果某列已存在筛选器，则新的筛选器将替换现有筛选器。如果某列没有筛选器，则 CALCULATE将新的筛选器添加到筛选上下文。
-   **新的筛选上下文建立后，CALCULATE 将其应用于模型，并基于此计算第一个参数：表达式。计算完成后，CALCULATE恢复原始筛选上下文，并返回计算结果。**

CALCULATE 还有另一个非常重要的用法：
**它将任何现有行上下文转换为筛选上下文。请记住：_CALCULATE_ 依据现有行上下文创建筛选上下文**。

CALCULATE 接受两种类型的筛选器：

-   **值列表**：在这种情况下，设计师需要提供希望在新的筛选上下文中显示的值的精确列表。筛选器可以是含任意数量列的表，它只考虑不同列中现有值的组合。
-   **布尔条件**：如 _Product[Color] = "White"_。因为结果需要单一列上的值列表，所以这些筛选器只在单一列上发挥作用。这种类型的筛选参数也称为谓词。

当使用带布尔条件的语法时，DAX 将其转换为值列表。因此，无论何时编写这段代码：

```js
Sales Amount Red Products :=
CALCULATE ( [Sales Amount], 'Product'[Color] = "Red " )
```

DAX 都将其转换为：

```js
Sales Amount Red Products :=
CALCULATE (
    [Sales Amount],
    FILTER ( ALL ( 'Product'[Color] ), 'Product'[Color] = "Red" )
)
```

由于 DAX 引擎对此的转换逻辑十分死板，故使用布尔条件筛选参数时，只能使用一列。DAX 引擎会去探测 FILTER 函数中迭代的列，而 FILTER 函数是由 DAX 引擎自动生成的。如果想在布尔表达式中引用两列或多列，则必须显式地编写 FILTER 迭代表达式，本章稍后将对此进行介绍。

> 请记住一条超级定理，该定理是 DAX 引擎底层进行筛选的核心基础，那就是 DAX 引擎对于表的筛选是基于值列表的，或者说值的集合，而它存在的形式是表，也就是， DAX 引擎在用表筛选表。用表筛选表，是 DAX 引擎筛选机制的核心原理。随着理解和实践的加深，也会更加透彻地体会到这里揭示的本质。

## 使用 CALCULATE 计算百分比

现在我们已经介绍了 _CALCULATE，可以用它定义几个计算。本节的目的是让您注意有关 CALCULATE_ 的一些细节，这些细节乍一看并不明显。在本章的后面，我们将讨论 _CALCULATE_ 的更高级的方面。现在，我们将重点放在开始使用 _CALCULATE_ 时可能遇到的一些问题上。

常见的模式是计算百分比。在处理百分比时，准确定义所需的计算是非常重要的。在这组示例中，您将了解如何使用 _CALCULATE_ 和 [[ALL]] 函数的不同用法来得到不同的结果。

我们可以从一个简单的百分比计算开始。我们希望构建以下报告，显示各类别的销售额及其占总销售额的百分比。您可以在图 5-8 中看到我们想要得到的结果。

![[Pasted image 20220629212009.png]]

**图** **5-8** _Sales Pct_ 显示了当前类别占总销售额的百分比

要计算百分比，就要用当前类别的销售额除以销售总额：当前类别的销售额为通过当前类别的筛选上下文计算得出的销售额；销售总额则是筛选上下文忽略对类别的筛选而计算得出的销售总额。实际上，音频（Audio）的 1.26%是 384,518.16 除以 30,591,343.98 得到的。

在报告的每一行中，筛选上下文已经包含当前类别。因此，给定的类别会自动对 _Sales Amount_ 进行筛选。比率的分母需要忽略当前筛选上下文来计算总金额。因为 CALCULATE 的筛选参数是表，所以提供一个表函数就足够了，它忽略当前作用于类别上的筛选上下文，并且总是返回所有类别（无论哪个筛选器）。您之前学过这个函数就是 _ALL_。请看下面的度量值：

```js
All Category Sales :=
CALCULATE (                              -- 更改销售额的
    [Sales Amount],                      -- 筛选上下文
    ALL ('Product'[Category] )           -- 使所有类别可见
)
```

ALL 从筛选上下文中清除 _Product[Category]_ 列上的筛选器。因此，在报表的任何单元格中，它都会忽略类别上存在的任何筛选。其效果是清除了对类别的筛选器。查看图 5-9 中的结果可以看到，对于 _All Category Sales_ 度量值，报告的每一行都返回相同的值，即 Total 对应的 _Sales Amount_（总销售额）。

![[Pasted image 20220629212017.png]]

**图** **5-9** _ALL_ 移除了对 _Category_ 的筛选，因此 _CALCULATE_ 使用了一个对 _Category_ 没有任何筛选器的筛选上下文。

_All Category Sales_ 度量值本身并没有用。用户不太可能希望创建一个对所有行显示相同值的报告。然而，这个值作为我们要计算的百分比的分母是完美的。事实上，计算百分比的公式可以这样写：

```js
Sales Pct :=
VAR CurrentCategorySales =                 -- CurrentCategorySales 包含
    [Sales Amount]                         -- 的销售额处于当前筛选上下文
VAR AllCategoriesSales =                   -- AllCategoriesSales 包含
CALCULATE (                                -- 的销售额处于所有类别均可见
    [Sales Amount],                        -- 的筛选上下文中
    ALL ( 'Product'[Category] )    
)
VAR Ratio =
DIVIDE ( 
    CurrentCategorySales,
    AllCategoriesSales
)
RETURN Ratio
```

正如在本例中所看到的，混合表函数和 CALCULATE 使得轻松编写有用的度量值成为可能。我们在书中经常使用这种技巧，这也是 DAX 中的主要技巧。

当用作 CALCULATE的筛选参数时，[[ALL]] 有特定的语义。事实上，它并没有用某列的所有值替换筛选上下文。相反，[[CALCULATE]] 使用 ALL 从筛选上下文清除了某列上的筛选器。这种行为的副作用有些复杂，不属于本介绍性部分。我们将在本章后面更详细地介绍它们。

正如在本节的介绍中所说，在编写类似的百分比时，重要的是要注意小细节。事实上，如果按类别划分报告，这个百分比可以很好地工作。公式清除了位于类别的筛选器，但它不触及任何其他现有筛选器。因此，如果报告添加了其他筛选器，那么结果可能不是想要的。例如，查看图 5-10 中的报告，在其中添加了 _Product[Color]_ 列，作为报告行中的第二级详细信息。

![[Pasted image 20220629212026.png]]

**图** **5-10** 在报告中添加颜色会在颜色级别产生意想不到的结果

查看百分比，类别级别的值是正确的，而颜色级别的值看起来是错误的。事实上，颜色百分比的和既不等于类别上的百分比小计，也不等于 100%。要理解这些值的含义以及它们是如何计算的，聚焦在某个单元格上并正确理解其筛选上下文的变化是很有帮助的，如图 5-11。

![[Pasted image 20220629212034.png]]

**图** **5-11** _ALL_ 在 _Product[Category]_上移除了类别上的筛选，但保留了颜色上的筛选。

由报告创建的原始筛选上下文包含类别筛选器和颜色筛选器。_CALCULATE_ 没有覆盖 _Product[Color]_上的筛选器，而是移除 _Product[Category]_上的筛选器。于是最终的筛选上下文只包含颜色。因此比率的分母是给定颜色（图 5-11 中的颜色是 Black）的所有类别的销售额。

这不是 _CALCULATE_ 的意外错误。而是这个公式被设计成只处理类别上的筛选，而不处理任何其他筛选。同样的公式在不同的报告中完全适用。看看如果切换列的顺序会发生什么：将颜色切片放在第一级，将类别切片放在第二级，如图 5-12 所示。

![[Pasted image 20220629212042.png]]

**图** **5-12** 颜色和类别互换后，结果看起来更合理。

图 5-12 中的报告更有意义。该度量值计算了相同的结果，但是由于报告的布局，它更加直观。所示的是在给定的颜色中，某一类别的销售额占所有类别销售额的百分比。在每个颜色内，类别百分比合计总是 100%。

换句话说，当要求用户计算百分比时，他们应该特别注意确定百分比的分母。_CALCULATE_ 和 _ALL_ 都是要使用的主要工具，但是公式如何编写取决于业务需求。

回到示例：目标是修正计算，使得公式既能计算类别也能计算颜色的百分比。有多种方式能够实现此操作，所有这些方法都会导致略有不同的结果，值得深入研究。

其中一个可能的解决方案是让 _CALCULATE_ 移除类别和颜色上的筛选器。添加多个筛选参数来让 _CALCULATE_ 实现这个目的：

```js
Sales Pct :=
VAR CurrentCategorySales = [Sales Amount]
VAR AllCategoriesAndColorSales =
    CALCULATE (
        [Sales Amount],
        ALL ( 'Product'[Category] ), -- 这两个 ALL 条件也可以写为
        ALL ( 'Product'[Color] )     -- ALL ( 'Product'[Category], 
    )                                -- 'Product'[Color] )
VAR Ratio =
    DIVIDE ( CurrentCategorySales, AllCategoriesAndColorSales )
RETURN
    Ratio
```

后一个版本的 Sales Pct 可以很好地处理包含颜色和类别的报告，但是它仍然受到与前一个版本类似的限制。实际上，它使用颜色和类别生成了正确的百分比(如图 5-13 所示)，但是一旦向报告添加其他列，它就会失败。

![[Pasted image 20220629212051.png]]

**图** **5-13** 使用 _ALL ( 'Product'[Category] )_和 _ALL ( 'Product'[Color] )_作为筛选参数，百分比现在可以正确地累加。

向报告中添加另一列将又会产生前面的问题。如果想创建移除 Product 表上的所有筛选器的百分比，仍然可以使用 _ALL_ 函数，并将整张 _Product_ 表作为 _ALL_ 的参数：

```js
Sales Pct All Products :=
VAR CurrentCategorySales = [Sales Amount]
VAR AllProductSales =
    CALCULATE ( [Sales Amount], ALL ( 'Product' ) )
VAR Ratio =
    DIVIDE ( CurrentCategorySales, AllProductSales )
RETURN
    Ratio
```

ALL 清除了 _Product_ 表上任何列上的任何筛选器。在图 5-14 中，您可以看到该计算的结果。

![[Pasted image 20220629212058.png]]

**图** **5-14** _ALL_ 清除了 _Product_ 表的所有列上的筛选器。

到目前为止，已经看到，通过使用 _CALCULATE_ 和 _ALL_，可以从一列、多列或整个表中移除筛选器。_CALCULATE_ 的真正强大之处在于，它为操纵筛选上下文提供多个选择，而且它的功能还不止于此。例如，用不同表中的列做切片来分析百分比。如果报告是按产品类别（Product[Category]）和客户所在大陆（Customer[Continent]）划分的，那么我们创建的最后一个度量值并不完美，如图 5-15 所示。

![[Pasted image 20220629212105.png]]

**图** **5-15** 使用不同表的列进行切片仍然显示了意外的结果。

在这一点上，问题可能很明显。分母上的度量值从 _Product_ 表中移除了所有筛选器，但保留了 Customer[Continent]上的筛选器。因此，分母计算给定大陆上所有产品类别的总销售额。

和前面的场景一样，可以移除多个表上的筛选器，方法是在 _CALCULATE_ 函数中添加多个筛选参数：

```js
Sales Pct All Products and Customers :=
VAR CurrentCategorySales = [Sales Amount]
VAR AllProductAndCustomersSales =
    CALCULATE ( [Sales Amount], ALL ( 'Product' ), ALL ( Customer ) )
VAR Ratio =
    DIVIDE ( CurrentCategorySales, AllProductAndCustomersSales )
RETURN
    Ratio
```

通过在两个表上使用 _ALL_，_CALCULATE_ 移除这两个表上的筛选器。正如预期的那样，百分比累加正确，如图 5-16 所示。

![[Pasted image 20220629212114.png]]

**图** **5-16** 使用两个表上的 ALL 同时移除两个表上的筛选上下文。

与两个列一样，两个表也面临同样的挑战。如果用户将第三个表中的某列添加到上下文，则度量值不会移除第三个表上的筛选器。当他们想要从任何可能影响计算的表中删除筛选器时，一个可能的解决方案是移除事实表本身的任何筛选器。在我们的模型中，事实表是 _Sales_。下面是计算可累加的百分比的度量值，不管与 Sales 表交互的是什么筛选器：

```js
Pct All Sales :=
VAR CurrentCategorySales = [Sales Amount]
VAR AllSales =
    CALCULATE ( [Sales Amount], ALL ( Sales ) )
VAR Ratio =
    DIVIDE ( CurrentCategorySales, AllSales )
RETURN
    Ratio
```

这个度量值利用关系移除任何可能筛选 _Sales_ 的表中的筛选器。在这个阶段，我们无法解释它如何工作的细节，因为它涉及到了扩展表原理，我们将在第 14 章“高级 DAX 概念”中介绍。您可以通过查看图 5-17 来了解它的行为，在图 5-17 中，我们从报告中移除了 _Sales Amount，并把 Calendar Year_ 拖到列上。请注意 _Calendar Year_ 属于 _Date_ 表，而在度量值中没有使用 _Date_ 表。不过，作为从 _Sales_ 表中移除筛选器的一部分，_Date_ 表上的筛选器被移除。

![[Pasted image 20220629212122.png]]

**图** **5-17** 事实表上的 _ALL_ 移除了相关表上的任何筛选器。

在结束使用百分数这个冗长的练习之前，我们想展示另一个操纵筛选上下文的示例。正如您在图 5-17 中所看到的，百分比总是与总额相比较，完全符合预期。如果目标是在当前年度的总金额上计算一个百分比呢？在这种情况下，需要仔细准备 _CALCULATE_ 创建的新筛选上下文。实际上，分母在计算销售总额时需要忽视除当前年份外的任何筛选器。_CALCULATE_ 需要做两件事：

-   **移除事实表上的所有筛选器**
-   **恢复年份筛选器**

注意，这两个条件同时进行，尽管看起来他们执行时可能是有先后顺序的。您已经了解了如何从事实表中移除所有筛选器。最后一步是学习如何恢复某个本来存在的筛选器。

本节的目的是解释操纵筛选上下文的基本技术。在本章的稍后部分，您将看到另一种更容易的方法来解决这个特定的需求，即使用 ALLSELECTED 计算可见总和的百分比。

[[VALUES]] 返回当前筛选上下文中列的值列表。因为 _VALUES_ 的结果是一个表，所以它可以用作 _CALCULATE_ 的筛选参数。因此，_CALCULATE_ 对给定列应用筛选器，将其值限制为由 _VALUES_ 返回的值。请看下面的代码：

```js
Pct All Sales CY :=
VAR CurrentCategorySales = [Sales Amount]
VAR AllSalesInCurrentYear =
    CALCULATE ( [Sales Amount], ALL ( Sales ), VALUES ( 'Date'[Calendar Year] ) )
VAR Ratio =
    DIVIDE ( CurrentCategorySales, AllSalesInCurrentYear )
RETURN
    Ratio
```

该度量每一年的合计为 100%，并计算针对当年所有销售额的百分比。如图 5-18 所示。

![[Pasted image 20220629212131.png]]

**图** **5-18** 通过使用 _VALUES_，您可以恢复并读取部分原始筛选上下文。

图 5-19 描述了这个复杂公式的全部行为。

![[Pasted image 20220629212137.png]]

**图** **5-19** 该图的关键是 _VALUES_ 仍然在原始筛选上下文中求值。

下面是对该图表的回顾：

-   内容是 4.22%的单元格（Calendar Year 2007，Cell Phones 的销售百分比）有筛选 CY 2007 和 Cell phones 的筛选上下文。
-   _CALCULATE_ 有两个筛选参数:_ALL ( Sales )_和 _V*_ALUES ( Date[Calendar Year] )*。
    -   ALL (Sales)从 _Sales_ 表中移除筛选器。
    -   *VALUES (Date[Calendar Year])_在原始筛选上下文中计算 VALUES_ 函数，仍然受列上 CY 2007 的影响。因此，它返回原始筛选上下文中唯一可见的年份：CY 2007。

_CALCULATE_ 的两个筛选参数应用于当前筛选上下文，使得筛选上下文只包含 _Calendar Year_ 的筛选器。分母在仅有 CY 2007 的筛选上下文中计算总销售额。

在调用 _CALCULATE_ 时，_CALCULATE_ 的筛选参数是原始筛选上下文中计算的，这一点至关重要。事实上，_CALCULATE_ 是在计算筛选参数之后才改变筛选上下文的。

在表格上使用 _ALL_，然后在列上使用 _VALUES_，是用筛选器清除整体但保留一个列上筛选上下文的一种常用技巧。



## 介绍 KEEPFILTERS

在前面的部分中您了解到CALCULATE的筛选参数覆盖同一列上的原始筛选上下文的已有筛选器。因此,以下度量值将忽略 _Product[Category]_ 上的已有筛选器，只返回 Audio 的销售额。

```js
Audio Sales :=
CALCULATE ( [Sales Amount], 'Product'[Category] = "Audio" )
```

如图 5-20 所示，对于 _Audio Sales_ 度量值，Audio 对应的 _Sales Amount_ 值在报告的所有行上重复。

![[Pasted image 20220629212147.png]]
**图** **5-20** _Audio Sales_ 总是显示 Audio 产品的销售情况，而忽略当前筛选上下文

_CALCULATE_ 应用新筛选器覆盖当前列上已存在的筛选器。筛选上下文的所有其余列都保持不变。如果不想覆盖现有的筛选器，可以使用 [[KEEPFILTERS]] 包裹筛选参数。例如，如果您想在 Audio 被选中的筛选上下文中显示 Audio 销售额，否则显示空白值，您可以编写以下度量值：

```js
Audio Sales KeepFilters :=
CALCULATE ( [Sales Amount], KEEPFILTERS ( 'Product'[Category] = "Audio" ) )
```

KEEPFILTERS修改 _CALCULATE_ 将筛选器应用于筛选上下文的方式。它不会覆盖同一列上的现有筛选器，而是将新筛选器追加到现有筛选器中。因此，只有筛选类别已经包含在筛选上下文中的单元格才会产生可见的结果。如图 5-21 所示。

![[Pasted image 20220629212155.png]]

**图** **5-21** _Audio Sales KeepFilters_ 仅在 Audio 行和总销售额显示 Audio 产品销售额。

正如它名字的含义，**KEEPFILTERS不覆盖现有筛选，而是保留现有筛选并将新筛选器追加到筛选上下文**。我们可以用图 5-22 描述该行为。

_KEEPFILTERS_ 不执行覆盖操作，它将 CALCULATE 筛选参数生成的新筛选器追加到上下文中。如果我们在 Cell Phones 行查看 _Audio Sales KeepFilters_ 度量值，得到的筛选上下文包含两个筛选器：Cell Phones 和 Audio。由于这两个条件的交集是一个空集，所以结果为空值。

![[Pasted image 20220629212202.png]]

**图** **5-22** 使用 _KEEPFILTERS_ 生成一个产品类别同时为 Cell phones 和 Audio 的筛选上下文

当一列中选择了多个元素时，_KEEPFILTERS_ 的行为会更清晰。例如，考虑以下度量值;它们分别在使用和不使用 _KEEPFILTERS_ 的情况下筛选 Audio 和 Computers：

```js
Always Audio-Computers :=
CALCULATE ( [Sales Amount], 'Product'[Category] IN { "Audio", "Computers" } )
KeepFilters Audio-Computers :=
CALCULATE (
    [Sales Amount],
    KEEPFILTERS ( 'Product'[Category] IN { "Audio", "Computers" } )
)
```

图 5-23 的报告显示，带有 _KEEPFILTERS_ 的版本只在 Audio 和 Computers 行显示销售额，其余类别都为空。汇总行只考虑 Audio 和 Computers。

![[Pasted image 20220629212209.png]]

**图** **5-23** 使用 _KEEPFILTERS_，原始的和新的筛选上下文被合并在一起

_KEEPFILTERS_ 既可以与谓词一起使用，也可以与表一起使用。实际上，前面的代码也可以用更详细的方式编写：

```js
KeepFilters Audio-Computers :=
CALCULATE (
    [Sales Amount],
    KEEPFILTERS (
        FILTER (
            ALL ( 'Product'[Category] ),
            'Product'[Category] IN { "Audio", "Computers" }
        )
    )
)
```

这仅是一个教学目的的例子。您应该使用最简单的谓词语法来写筛选参数。在筛选单个列时，不必使用 _FILTER_ 显式形式。但是，稍后您将看到，复杂的筛选条件需要使用 _FILTER_ 显式形式。在这些情况下，可以围绕 _FILTER_ 函数显式形式使用 _KEEPFILTERS_ 修改器。如下一节所示。

## 筛选单列

上一节，我们介绍了在 _CALCULATE_ 中引用单个列作为筛选参数。需要注意的是，在一个表达式中可以多次引用同一列。比如，下面的度量值语法是有效的，它两次引用同一列_(Sales[Net Price])_。

```js
Sales 10-100 :=
CALCULATE ( [Sales Amount], Sales[Net Price] >= 10 && Sales[Net Price] <= 100 )
```

事实上，它可以转换成以下语法：

```js
Sales 10-100 :=
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( Sales[Net Price] ),
        Sales[Net Price] >= 10
            && Sales[Net Price] <= 100
    )
)
```

由 _CALCULATE_ 生成的筛选上下文只在 *Sales[Net Price]_列上添加一个筛选器。关于用谓词作为 CALCULATE_ 中的筛选参数的注意事项是，尽管它们看起来像条件，但其实它们是表。在前面两个 _Sales 10-100_ 度量值的写法中，第一种写法 _CALCULATE_ 看起来是在计算一个条件，而实际上，它是按第二种写法 _CALCULATE_ 计算 *Sales [Net Price]*中所有满足条件的列表。然后 _CALCULATE_ 将该表应用于模型筛选。

当两个条件之间是逻辑 _AND_ 的关系时，可将它们表示为两个单独的筛选器。事实上，前面的表达式与下面的是等价的：

```js
Sales 10-100 :=
CALCULATE ( [Sales Amount], Sales[Net Price] >= 10, Sales[Net Price] <= 100 )
```

但请记住，_CALCULATE_ 的多个筛选参数之间总是逻辑 _AND_ _的关系_。因此在处理逻辑 _OR_ 的关系时，必须使用单个筛选参数，如下所示：

```js
Sales Blue+Red :=
CALCULATE (
    [Sales Amount],
    'Product'[Color] = "Red" || 'Product'[Color] = "Blue"
)
```

两个或多个独立的筛选参数会合并为单一的筛选上下文。下面的度量值总是产生一个空值，因为没有一个产品既是 Blue 同时又是 Red:

```js
Sales Blue and Red :=
CALCULATE (
    [Sales Amount],
    'Product'[Color] = "Red",
    'Product'[Color] = "Blue"
)
```

实际上，上面的度量值与如下只使用单一筛选参数的度量值是一样的：

```js
Sales Blue and Red :=
CALCULATE (
    [Sales Amount],
    'Product'[Color] = "Red" && 'Product'[Color] = "Blue"
)
```

筛选参数总是返回筛选上下文中颜色为空的列表。所以度量值总是返回一个空值。

只要筛选参数引用单个列，都可以使用谓词。我们建议您这样做，因为这样的代码容易阅读。对于筛选参数之间为逻辑 _AND_ 时亦然。然而，永远不要忘记您只是在依赖语法糖。实际上 _CALCULATE_ 是基于表工作的，尽管它的简化形式看起来并非如此。

另一方面，当一个筛选参数含两个或多个不同的列引用时，必须应用 _FILTER_ 函数返回表。下一节中您将学习这一点。

## 复杂条件筛选

引用多个列的筛选参数需要明确地使用”表“表达式，所以有必要了解编写表筛选器的不同方法。但请记住，创建筛选时使用谓词，尽可能的使用最少的列不失为最佳实践。

考虑这样一个度量值，它仅对销售金额大于或等于 1000 的交易进行求和，这需要先将 _Quantity_ 和 _Net Price_ 列相乘。这是因为在 Contoso 数据库中，_Sales_ 表中没有专门存储销售额的列。您可能会尝试编写类似以下表达式，但不幸的是，这个表达式不起作用：

```js
Sales Large Amount :=
CALCULATE ( [Sales Amount], Sales[Quantity] * Sales[Net Price] >= 1000 )
```

此代码无效，因为筛选参数在一个表达式中引用了两个不同列。DAX 不能自动将其转换成合适的 _FILTER_ 表达式。最佳方法是使用明确的表，其只包含谓词中引用列的已存在且不重复的组合:

```js
Sales Large Amount :=
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( Sales[Quantity], Sales[Net Price] ),
        Sales[Quantity] * Sales[Net Price] >= 1000
    )
)
```

_FITLER_ 函数创建的筛选上下文包含两列以及符合组合筛选条件的非重复行（代码第 6 行）如图 5-24 所示。

![[Pasted image 20220629212222.png]]

**图** **5-24** 只包含 *Sales[Quantity]_Sales [Net Price]的>=1000_ 的多列筛选器。

该筛选器的结果如图 5-25 所示。

![[Pasted image 20220629212228.png]]

**图** **5-25** _Sales Large Amount_ 仅显示销售额 _>=1000_ 的销售总和。

注意图 5-25 中的切片器没有筛选任何值:其所显示的两个值是 _Net Price_ 的最小值和最大值。接下来解释度量值如何与切片器交互作用。像 _Sales Large Amount_ 这样的度量值，在覆盖 _Quantity_ 或 _Net Price_ 的已有筛选器时需要注意。实际上，因为筛选参数对两列使用 ALL，所以同一列上所有已有筛选器均被忽略，包括本例中的切片器。图 5-26 中切片器在 500 到 3000 对 Net Price 进行筛选，但它的结果与图 5-25 相同，结果令人惊讶。

![[Pasted image 20220629212234.png]]

**图** **5-26** 切片器所选价格范围内没有 _Audio_ 的销售，但 _Sales Large Amount_ 却仍显示值。

_Audio_ 和 _Music, Movies and Audio Books_ 的 _Sales Large Amount_ 的显示结果与期望不符。实际上，在切片器筛选的区间内，这两个类别没有销售但 _Sales Large Amount_ 度量值仍有结果显示。

原因是 _Sales Large Amount_ 度量值忽略了切片器创建的 _Net Price_ 筛选上下文，该度量值覆盖了 _Quantity_ 和 _Net Price_ 的现有筛选。如果仔细比较图 5-25 和图 5-26，会发现 _Sales Large Amount_ 的值是相同的，就好像没有将切片器添加到报告中一样。事实上，_Sales Large Amount_ 完全忽略了切片器。

如果您关注某个单元格，比如 _Audio_ 的 _Sales Large Amoun_t，计算其值的代码如下:

```js
Sales Large Amount :=
CALCULATE (
    CALCULATE (
        [Sales Amount],
        FILTER (
            ALL ( Sales[Quantity], Sales[Net Price] ),
            Sales[Quantity] * Sales[Net Price] >= 1000
        )
    ),
    'Product'[Category] = "Audio",
    Sales[Net Price] >= 500
)
```

从这段代码中您可以看到，最内层的 _ALL_ 函数忽略了最外层 _CALCULATE_ 函数所创建的的应用于 _Sales[Net Price]_的筛选器。在这种情况下，可以使用 KEEPFILTERS 来避免现有筛选器被覆盖:

```js
Sales Large Amount KeepFilter :=
CALCULATE (
    [Sales Amount],
    KEEPFILTERS (
        FILTER (
            ALL ( Sales[Quantity], Sales[Net Price] ),
            Sales[Quantity] * Sales[Net Price] >= 1000
        )
    )
)
```

新的 _Sales Large Amount KeepFilter_ 度量值产生的结果如图 5-27 所示。

![[Pasted image 20220629212243.png]]

**图** **5-27** 使用 _KEEPFILTERS_，外部切片器被考虑到计算中

指定复杂筛选器的另一种方法是使用表筛选器而不是列筛选器。这是 DAX 新手比较喜欢的技术之一，尽管使用它非常危险。事实上，前面的度量值可以使用表筛选器来编写:

```js
Sales Large Amount Table :=
CALCULATE (
    [Sales Amount],
    FILTER ( Sales, Sales[Quantity] * Sales[Net Price] >= 1000 )
)
```

您可能还记得，_CALCULATE_ 的所有筛选参数都是在 _CALCULATE_ 函数的外部筛选上下文中计算的。因此，对 _Sales_ 的迭代只考虑在现有筛选上下文中的可见行，通过 _Net Price_ 列筛选创建。因此，度量值 _Sales Large Amount Table_ 与 _Sales Large Amount KeepFilter_ 结果相同。

虽然看起来很简单，但是在使用表筛选器这种技术时应该小心，因为它可能会严重影响性能或结果的准确性。具体我们将在第 14 章讨论这些细节。现在请记住，最佳实践总是使用尽可能少的列作筛选器。

此外，您应该避免使用表筛选器。因为 _Sales_ 表可能非常大，逐行扫描它来计算谓词可能是非常耗时的操作。另一方面，_Sales Large Amount KeepFilter_ 只迭代 _Quantity_ 和 _Net Price_ 满足筛选条件且不重复的组合，这个数字通常比整个 _Sales_ 表的行数小得多。

## CALCULATE中的计算顺序

任何时候查看 DAX 代码，正常的计算顺序都是先从最内部开始。例如，下面这个表达式：

```js
Sales Amount Large :=
SUMX (
    FILTER ( Sales, Sales[Quantity] >= 100 ),
    Sales[Quantity] * Sales[Net Price]
)
```

**在对 _SUMX_ 进行计算之前，DAX 先计算 _FILTER_ 的结果。事实上，_SUMX_ 迭代一个表。由于该表是 FILTER 的结果，所以在 _FILTER_ 返回其结果之后，_SUMX_ 才可以开始迭代。这个规则适用于除了 _CALCULATE_ 和 _CALCULATETABLE_ 之外所有的 DAX 函数。实际上，_CALCULATE_ 首先计算它的筛选参数，最后才计算第一个参数，即用于 _CALCULATE_ 计算结果的表达式**。

此外，由于 _CALCULATE_ 可以更改筛选上下文，所以事情会更复杂些。所有的筛选参数都在 _CALCULATE_ 外部的筛选上下文中执行，并且每个筛选器都是独立计算。在同一个 _CALCULATE_ 中，筛选器的顺序并不重要。因此，下列各项度量值完全相同：

```js
Sales Red Contoso :=
CALCULATE (
    [Sales Amount],
    'Product'[Color] = "Red",
    KEEPFILTERS ( 'Product'[Brand] = "Contoso" )
)

Sales Red Contoso :=
CALCULATE (
    [Sales Amount],
    KEEPFILTERS ( 'Product'[Brand] = "Contoso" ),
    'Product'[Color] = "Red"
)

Sales Red Contoso :=
VAR ColorRed =
    FILTER ( ALL ( 'Product'[Color] ), 'Product'[Color] = "Red" )
VAR BrandContoso =
    FILTER ( ALL ( 'Product'[Brand] ), 'Product'[Brand] = "Contoso" )
VAR SalesRedContoso =
    CALCULATE ( [Sales Amount], ColorRed, KEEPFILTERS ( BrandContoso ) )
RETURN
    SalesRedContos
```

使用变量定义的 _Sales Red Contoso_ 版本比其他版本更冗长，但在碰到带有显式筛选器的复杂表达式时，你可能需要用到它。这样，就更容易理解筛选器是在 _CALCULATE_“之前”被计算的。

对于嵌套 _CALCULATE_ 语句，这条规则变得更加重要。实际上，首先应用的是最外层的筛选器，然后才是最内层的筛选器。理解嵌套 _CALCULATE_ 语句的行为非常重要，因为每次嵌套度量值调用时都会遇到这种情况。例如，考虑以下度量值，其中 _Sales Green_ 调用 _Sales Red_:

```js
Sales Red :=
CALCULATE ( [Sales Amount], 'Product'[Color] = "Red" )

Green calling Red :=
CALCULATE ( [Sales Red], 'Product'[Color] = "Green" )
```

为了使嵌套的度量调用更加明显，我们可以这样扩展 _Sales Green_:

```js
Green calling Red Exp :=
CALCULATE (
    CALCULATE ( [Sales Amount], 'Product'[Color] = "Red" ),
    'Product'[Color] = "Green"
)
```

计算顺序如下:

1.  首先，外部 _CALCULATE_ 应用筛选器，_Product[Color] = "Green"_。
2.  其次，内部 _CALCULATE_ 应用筛选器，_Product[Color] = "Red"_。此筛选器覆盖前一个筛选器。
3.  最后，DAX 使用 _Product[Color] = “Red”_筛选器计算_[Sales Amount]_ 。

因此，_Red_ 和 _Green calling Red_ 的结果都是 _Red_，如图 5-28 所示。

![[Pasted image 20220629212254.png]]

**图 5-28** 后三个度量返回相同的结果，即始终为红色产品的销售额

上述描述仅用于教学目的。实际上，引擎对筛选上下文使用惰性(Lazy)计算。因此，如果存在筛选参数覆盖(如前面的代码)，则可能永远不会计算外部筛选器，因为这没有任何用处。不过，此行为仅用于优化。它不会以任何方式改变 _CALCULATE_ 的语义。

我们可以通过另一个示例来查看计算的顺序以及筛选上下文如何计算的。考虑以下度量值:

```js
Sales YB :=
CALCULATE (
    CALCULATE ( [Sales Amount], 'Product'[Color] IN { "Yellow", "Black" } ),
    'Product'[Color] IN { "Black", "Blue" }
)
```

图 5-29 展示了 _Sales YB_ 中筛选上下文的计算。

![[Pasted image 20220629212301.png]]

**图 5-29** 最里面的筛选器覆盖了外部筛选器

如前所述，*Product[Color]上最内层的筛选器覆盖了最外层的筛选器。因此，度量值结果显示的是 Yellow 和 Black 产品的总和。通过对最里层的 *CALCULATE 函数使用 KEEPFILTERS，最终的筛选上下文是将两个筛选器合并而非用新的筛选器覆盖已有筛选器:

```js
Sales YB KeepFilters :=
CALCULATE (
    CALCULATE (
        [Sales Amount],
        KEEPFILTERS ( 'Product'[Color] IN { "Yellow", "Black" } )
    ),
    'Product'[Color] IN { "Black", "Blue" }
)
```

图 5-30 展示了 _Sales YB KeepFilters 中_筛选上下文的计算。

![[Pasted image 20220629212307.png]]

**图 5-30** 使用 _KEEPFILTERS_, _CALCULATE_ 不会覆盖已有的筛选上下文

由于这两个筛选器同时保留，它们互相取交集。因此，在新的筛选上下文中，唯一可见的颜色是 Black，因为它是唯一出现在两个筛选器中的颜色。

同一个 CALCULATE 中的筛选参数的顺序无关紧要，因为它们独立应用于筛选上下文。