---
created: 2022-06-26
tags: 
- dax圣经 
- 上下文
subject: dax圣经
importance: 5
skilled: 4
status: 
author: sqlbi
url: 
cover: 
---


下面的 DAX 度量值：

```js
IncreasedSales := SUMX ( Sales, Sales[Net Price] * 1.1 )
```

[[SUMX]] 是一个迭代函数，所以 _SUMX_ 会在 _Sales_ 表上创建一个行上下文，并在迭代期间使用它。 行上下文迭代 _Sales_ 表（第一个参数），并在迭代期间将当前行提供给第二个参数。 换句话说，**DAX 根据第一个参数中当前迭代行的行上下文计算内部表达式（SUMX的第二个参数）。**

**SUMX 的两个参数使用不同的上下文**。 事实上，DAX 代码的每一部分都是在它被调用时的上下文中工作。 因此，当执行表达式的时候，可能已经存在筛选上下文以及一个或多个处于活动状态中的行上下文。参见如下代码中的注释。

```js
SUMX (
    Sales,                 -- 外部筛选上下文和行上下文
    Sales[Net Price] * 1.1 -- 外部筛选上下文、行上下文 以及新的行上下文
)
```

第一个参数 _Sales_ 的上下文内容是根据度量值被调用时所处的环境决定的。第二个参数(表达式)的结果是同时依据外部上下文以及新创建的行上下文决定的。

所有迭代器函数的执行方式都一样:
1.  **根据当前上下文计算第一个参数以确定需要扫描的行。**
2.  **为在上一步中确定表的每一行创建一个新的行上下文。**
3.  **迭代整张表并在已确定的上下文中执行第二参数表达式,已确定的上下文包括新创建的行上下文。**
4.  **聚合上一步中计算的值。**

请记住原始上下文在表达式中依然有效。迭代器只是添加了一个新的行上下文；它们并不修改已经存在的筛选上下文。比如，如果外部的筛选上下文包含对于颜色为红色的筛选，这个筛选在整个迭代过程中依然有效。

行上下文迭代，而不进行筛选。所以，在任何情况下，我们都不能使用迭代器函数覆盖外层的筛选上下文。

以上规则始终有效，但还有一些非常重要的细节。如果在前一个上下文中已经包含了行上下文，则在同一张表上新创建的行上下文将会隐藏之前的行上下文。

## 不同表上的嵌套行上下文

迭代器中要计算的表达式可能非常的复杂。并且表达式本身还包含新的迭代。乍一看，在某个迭代过程中开始另一个迭代可能会显得非常奇怪。尽管如此，这在 DAX 中很常见，因为迭代器嵌套可以生成非常强大的表达式。

例如，以下代码包含三个嵌套迭代器，它扫描三个表：_Categories_，_Products_ 和 _Sales_。

```js
SUMX (
     'Product Category',                 --  扫描 Product Category 表
      SUMX (                             --  针对每一个产品分类
          RELATEDTABLE ( 'Product' ),    --  扫描对应产品分类的产品
          SUMX (                         --  针对每个产品
              RELATEDTABLE ( Sales )     --  扫描对应产品的销售
              Sales[Quantity]            --
                * 'Product'[Unit Price]  --  计算该产品的销售金额
                * 'Product Category'[Discount]
           )
     )
)
```

最里面的表达式 ——三个因子的乘法 ———引用了三张表。事实上，三个行上下文在表达式计算期间都有效：每个行上下文均代表当前正在被迭代的表。值得注意的是**两个 [[RELATEDTABLE]] 函数返回在当前行上下文中关联表的行**。因此, _RELATEDTABLE_ _(_ _Product_ _)_ 是在_Categories_ 表的行上下文中被执行，返回指定产品类型对应的产品。基于同样的原则，_RELATEDTABLE ( Sales )_ 返回指定产品对应的销售。

前一段代码就运行效率和可读性来说还不是最优化的。通常，只要扫描的行数不是太大那么使用嵌套迭代器是可行的：几百行的数据没问题，几千也还可以，几百万行就不可行了。否则，很容易就遇到运行效率问题。我们使用前一段代码来演示创建多个嵌套迭代器是可行的; 在本书的后面，我们会看到更多的嵌套迭代器有用例子。

下面的代码基于独立行上下文和 _RELATED_ 函数实现与前一段代码相同的功能，且执行的速度更快，代码也更加易读。

```js
SUMX (
    Sales,
    Sales[Quantity]
        * RELATED ( 'Product'[Unit Price] )
        * RELATED ( 'Product Category'[Discount] )
)
```

只要对于在不同表中的行上下文，均可以在一个 DAX 表达式中引用被迭代的表。然而有一种情况可能会带来些挑战。那就是在同一张表中多个行上下文的嵌套。

## 同一表上的嵌套行上下文

试想我们打算按产品价格对产品进行排名。最贵产品应该排名第1，第二贵的产品应该排名第2，以此类推。我们可以使用 RANKX 函数实现该功能。但从教学目的来说，我们使用比较简单的DAX函数来实现该功能。

为了计算排名，针对每个产品我们都要计算有多个价格比它高的产品。如果没有比当前产品价格高的产品，则当前产品最贵同时也就排名第1位。如果有一个产品的价格比当前产品高，则当前产品排名第2。事实上，我们正在做的是通过统计有多少价格比当前产品价格高并对结果加1，以此来计算每个产品的排名。

因此，我们可以使用如下代码创建计算列，其中我们使用 _**PriceOfCurrentProduct**_ 作为占位符来表示当前产品的价格。

```js
'Product'[UnitPriceRank] =
COUNTROWS (
     FILTER (
         'Product',
         'Product'[Unit Price] > PriceOfCurrentProduct
     )
 ) + 1
```

[[FILTER]] 函数返回所有价格高于当前产品价格的产品，COUNTROWS 函数计算 _FILTER_ 函数结果。 唯一的问题是找到一种方法来表达当前产品的价格，并用有效的DAX语法替换 _**PriceOfCurrentProduct**_。 “当前”是指DAX计算列时当前行中列的值。 而这比你想象的要难。

请将注意看上段代码的第5行。 在那里，对 _Product [Unit Price]_ 的引用是指当前行上下文中 _Unit Price_ 的值。 当DAX代码执行到第5行时，活动的行上下文是什么？有两个行上下文。 由于代码是在计算列中编写的，因此引擎会自动创建一个默认行上下文，用于扫描 _Product_ 表。 此外，_FILTER_ 函数是一个迭代器，_FILTER_ 生成的行上下文再次扫描产品表。

在运行最内层表达式期间，在同一个表上同时有两个行上下文。

![[Pasted image 20220626225619.png]]

外层包含计算列的行上下文，该行在 _Product_ 上进行迭代。 但是，内层使用了 _FILTER_ 函数的行上下文，它也在Product上进行迭代。 表达式 _Product[Unit Price]取决于上下文。 因此，内层中对 Product [Unit Price]_ 的引用只能通过 _FILTER_ 函数引用当前迭代的行。 问题是，在内层中，我们需要计算在计算列的行上下文引用的 _Unit Price_ 的值，但该行现在已隐藏。

实际上，当不使用迭代器创建一个新的行上下文时，_Product [Unit Price]_ 的值就是需要的值，它是计算列的当前行上下文中的值，就像在这段简单的代码中一样：

```js
Product[Test] = Product[Unit Price]
```

为了进一步证明这一点，让我们使用一些虚拟代码在两个部分中计算 _Product[Unit Price]_

![](https://secure2.wostatic.cn/static/5pxoao9kxuP26kVScswcQ3/image.png)

以下是目前为止的小结：

-   **由FILTER函数生成的内部行上下文隐藏外部行上下文。**
-   **我们需要将内层 _****Product[Unit Price]****_ 与外层 _****Product[Unit Price]****_ 的值进行比较。**
-   **如果我们在内层表达式中执行比较，我们将无法访问外层 _****Product[Unit Price]****_。**

因为，如果在 _FILTER_ 函数的之外的行上下文中，我们是可以取得当前单价的，那么解决此问题的最佳方法是在变量中保存 _Product [Unit Price]_ 的值。 实际上，可以使用以下代码计算计算列的行上下文中变量的值：

```js
'Product'[UnitPriceRank] =
VAR
    PriceOfCurrentProduct = 'Product'[Unit Price]
RETURN
    COUNTROWS (
        FILTER (
            'Product',
            'Product'[Unit Price] > PriceOfCurrentProduct
      )
    ) + 1
```

此外，通过使用更多变量来分解计算的不同步骤，以更具描述性的方式编写更好的代码。 这样，代码也更容易理解：

```js
'Product'[UnitPriceRank] =
VAR PriceOfCurrentProduct = 'Product'[Unit Price]
VAR MoreExpensiveProducts =
    FILTER (
        'Product',
        'Product'[Unit Price] > PriceOfCurrentProduct
    )
RETURN
    COUNTROWS ( MoreExpensiveProducts ) + 1
```

![[Pasted image 20220626225635.png]]

因为有14个产品的单价相同，所以它们的排名始终是1; 第十五产品的单价与其他产品的单价相同所以排名为15。 如果我们能够让排名结果为1,2,3而不是1,15,19 那就会很好，就像下图中的情况一样。 我们很快就会解决这个问题，但在此之前，重要的是做一个题外话。

为了处理类似上述场景的案例，有必要对是什么行上下文有充分的理解，以便能够在公式的不同部分中检测哪个行上下文是活动的，并且最重要的是，理解行上下文如何影响DAX表达式返回的值。 值得强调的是，在公式的两个不同部分中评估的相同表达式 _Product [Unit Price]_ 由于评估它所处的不同行上下文而返回不同的值。

在结束这个例子之前，我们需要解决最后的细节 - 也就是说，使用1,2,3的序列格式进行排序，而不是目前的序列格式。 解决方案比预期的要容易。 事实上，在之前的代码中，我们专注于使用高价格统计产品个数。 这样做的结果是，该公式计算出有14个产品排名第1，并将15个产品分配到排名第2。 但是，统计产品并不是很有用。 如果公式计算的价格高于当前价格而不是产品，则所有14种产品将变成单一价格。

```js
'Product'[UnitPriceRankDense] =
VAR PriceOfCurrentProduct = 'Product'[Unit Price]
VAR HigherPrices =
    FILTER (
        VALUES ( 'Product'[Unit Price] ),
        'Product'[Unit Price] > PriceOfCurrentProduct
    )
RETURN
    COUNTROWS ( HigherPrices ) + 1
```

最后一小步是计算价格而不是计算产品，这似乎比预期更难。 使用DAX的次数越多，就越容易开始考虑为实现计算目的而创建临时表。

在这个例子中，您了解到在同一个表上处理多行上下文的最佳方法是使用变量。 请记住，变量是在2015年之后 在DAX语言中引入的。您可能会发现在变量时代之前编写的DAX代码 - 使用另一种技术来访问外部行上下文：_EARLIER_ 函数

## EARLIER

DAX 提供了一个访问外层行上下文的函数：_EARLIER_。**EARLIER 读取前一个行上下文中的列值而不是最近的行上下文的列值**。 因此，我们可以使用 EARLIER（Product [UnitPrice]）赋值给变量 _PriceOfCurrentProduct_。

例如，以下代码在不使用变量的情况下解决了上一个场景的问题：

```js
'Product'[UnitPriceRankDense] =
COUNTROWS (
    FILTER (
        VALUES ( 'Product'[Unit Price] ),
        'Product'[UnitPrice] > EARLIER ( 'Product'[UnitPrice] )
    )
) + 1
```

> **注** _EARLIER_ 的第二个参数，即要指出需要跳过的层数，以便可以跳过2个或更多层行上下文。此外，还有一个名为 _EARLIEST_ 的函数，它允许在当前行上下文中访问最外层的行上下文。在现实情况中，_EARLIER_ 的第二个参数以及 _EARLIEST_ 函数很少使用。尽管在计算列中使用两层嵌套的行上下文是很常见的情况，但几乎不会用到三层或以上。此外，**自从变量 _VAR_ 出现以后，_EARLIER_ 可以被彻底取代**。