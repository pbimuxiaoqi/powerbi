---
created: 2022-07-10
tags: 
- dax圣经 
- dax进阶 
- 变量
subject: dax圣经
importance: 1
skilled: 5
status:
author: sqlbi
url:
cover: 
---

```dataview
list url
where file.name = this.file.name
```


当我们在一个表达式里面引入变量时，需要了解的第一个关键词就是 VAR 了，它定义了变量，紧随其后的就是 RETURN 了，它定义了要返回的结果。接下来你会看到一个包含变量的经典表达式。

```js
VAR SalesAmt =
    SUMX (
        Sales,
        Sales[Quantity] * Sales[Net Price]
    )
RETURN
    IF (
        SalesAmt > 100000,
        SalesAmt,
        SalesAmt * 1.2
    )
```

在同一个 VAR/RETURN 块中添加更多的 VAR 可以定义许多个变量，然而 RETURN 只能有一个。值得我们注意的是，VAR/RETURN 这个组合其实已经构成了一个完整的表达式了。 无论在哪里，只要这个表达式被使用了，这个变量的定义就产生了意义。这使得在一个迭代器里面或者在一个更复杂的表达式里面定义变量成为可能，就像接下来的这个案例。

```js
VAR SalesAmt =
SUMX (
     Sales,
        VAR Quantity = Sales[Quantity]
        VAR Price = Sales[Price]
        RETURN
            Quantity * Price
    )
RETURN 
    ...
```

在写一个度量值的公式的时候，变量通常被定义在最前面，然后在整段代码中，都可以拿来被使用。然而，这仅仅是一种写作习惯而已，不把它写在最前面，写在其他嵌套的公式里面也是比较常见的。就像刚才的那个案例，Quantity 和 Price 这两个变量被分配给了 Sales table 的每一行，在这个 SUMX 的迭代器里面。在迭代器外面，这两个变量是不能够被使用的。

一个变量，既可以是一个标量值，也可以是一张表。 而且这些被定义的变量在一个表达式里面经常什么都有。多个变量可以定义在同一个 VAR/RETURN 块内，且可以是不同类型的（ VAR 了一张表，RETURN 了一个标量值，反之亦可）。

一个关于变量的常见用法是，把一个复杂的计算表达式，按照逻辑一步步拆分开来，其中把每一步的结果都分配给一个变量。举例来讲，下面这段代码就是把计算过程中的部分结果储存在了变量里面。

```js
Margin% :=
VAR SalesAmount =
    SUMX ( Sales, Sales[Quantity] * Sales[Net Price] )
VAR TotalCost =
    SUMX ( Sales, Sales[Quantity] * Sales[Unit Cost] )
VAR Margin = 
    SalesAmount - TotalCost
VAR MarginPerc =
    DIVIDE ( Margin, TotalCost )
RETURN
    MarginPerc
```

同样目的的表达式，没有变量的话，需要花费更多注意力去阅读:

```js
Margin% :=
DIVIDE (
    SUMX (
        Sales,
        Sales[Quantity] * Sales[Net Price]
    ) - SUMX (
            Sales,
            Sales[Quantity] * Sales[Unit Cost]
        ),
    SUMX (
        Sales,
        Sales[Quantity] * Sales[Unit Cost]
    )
)
```

此外，这个版本的变量有一个优势，就是每个变量仅仅被计算一次即可，举个例子，TotalCost 被用在了两个不同的部分，但是由于它被定义作为一个变量，所以 DAX，确保了它的计算只被计算一次。

你可以在 RETURN 的后面写任何表达式，然而，在 RETURN 后面只用一个变量被认为是最好的方式。
