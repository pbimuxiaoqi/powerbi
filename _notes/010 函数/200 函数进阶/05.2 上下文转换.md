---
created: 2022-06-29
tags: 
- 上下文 
- calculate 
- dax圣经
subject: dax圣经
importance: 5
skilled: 3
status:
author: sqlbi
url:
cover: 
---

# 5.2 上下文转换

回顾**上下文转换**，定义如下:
_CALCULATE_ 使任何行上下文无效。它自动将任何行上下文中正在迭代的所有列——在迭代行中筛选出它们的实际值——添加为筛选参数。


## 行上下文和筛选上下文重述

借助图 5-31 我们复习关于行上下文和筛选上下文的一些重要知识，图 5-31 显示了 _Brand_ 在行上的报告及其计算过程。图表中的 _Products_ 和 _Sales_ 并非真实的数据，仅通过若干行来清楚地描述计算过程。

![[Pasted image 20220629222546.png]]

**图** **5-31** 描述了使用 _SUMX_ 执行一个简单迭代的完整流程

如下关于图 5-31 的描述有助于理解 _Contoso_ 行上 _Sales Amount_ 度量值的整个计算过程：

-   报告创建一个筛选上下文，其中包含一个 _Product[Brand] = “ Contoso ”_的筛选器。
-   筛选器适用于整个模型，筛选 _Product_ 和 _Sales_ 表。
-   筛选上下文减少了扫描 _Sales_ 时 _SUMX_ 迭代的行数。_SUMX_ 只迭代与 _Contoso_ 产品相关的销售行。
-   图中产品名称为 _Contoso_ 的 A 产品在 _Sales_ 表中为两行。
-   因此，_SUMX_ 迭代两行。第一行计算 1_11.00，结果为 11.00。第二行计算 2_10.99，结果为 21.98。
-   _SUMX_ 返回上述迭代结果的合计。
-   在对 _Sales_ 进行迭代时，_SUMX_ 只扫描 _Sales_ 表的可见部分，并为每个可见行生成一个行上下文。
-   _SUMX_ 迭代第一行时，_Sales[Quantity]_ = 1, _Sales[Net Price]_ = 11。在第二行，值是不同的。当前列的值取决于正在迭代的行。对所有列来说，每一个被迭代的行可能有不同的值。
-   在迭代过程中，有一个行上下文和一个筛选上下文。筛选上下文仍然是筛选 _Contoso_ 的筛选上下文，因为没有执行任何 _CALCULATE_ 来修改它。

说到上下文转换，最后一句话是最重要的。在迭代期间，筛选上下文仍然是被激活的，它筛选 _Contoso_。另一方面，行上下文目前正在迭代 _Sales_ 表。_Sales_ 的每一列都有一个给定的值。行上下文通过当前行提供值。记住，行上下文迭代;而筛选上下文不迭代。

这是一个重要的细节。请你通过下面度量值来再次检验自己的理解。假设你创建了一个度量值对 _Sales_ 表行数进行计数，代码如下：

```js
NumOfSales := COUNTROWS ( Sales )
```

在报表中使用后，该度量值将计算当前筛选上下文中可见的 _Sales_ 行数。图 5-32 所示的结果符合预期：每个品牌都有一个不同的数字。

![[Pasted image 20220629222556.png]]

**图** **5-32** _NumOfSales_ 统计 _Sales_ 表中当前筛选上下文中可见的行数

因为 _Contoso_ 品牌的 _Sales_ 中有 37,984 行，这意味着对品牌为 _Contoso_ 的 _Sales_ 表进行迭代时将精确地迭代这 37,984 行。前面 _Sales Amount_ 度量值是在 37,984 次乘法之后再进行求和。

根据目前为止所获得的理解，您能猜到以下 _Contoso_ 行的度量值的结果吗?

```js
Sum Num Of Sales := SUMX ( Sales, COUNTROWS ( Sales ) )
```

不要急着给出答案。慢慢来，仔细研究这个简单的代码，并做出有根据的猜测。在下一段中，我们给出了正确的答案。

从前面的示例可以了解到，筛选上下文在筛选了 _Contoso_ 后，_SUMX_ 迭代 37,984 次。对于这 37,984 行中的每一行，_SUMX_ 计算当前筛选上下文中 _Sales_ 中可见的行数。筛选上下文仍然是相同的，所以对于每一行，[[COUNTROWS]] 的结果都是 37,984。因此，_SUMX_ 将 37,984 这个值求和 37,984 次。结果是 37984 的平方。您可以从图 5-33 中度量值的结果来确认这一点。

![[Pasted image 20220629222604.png]]

**图** **5-33** _Sum Num Of Sales_ 计算 _NumOfSales_ 的平方，因为它每次迭代均对所有行计数

## 介绍上下文转换

当表上发生迭代时，行上下文就存在。迭代内部的表达式依赖于行上下文本身。下面这个表达式，您已经学过很多次了，很有用：

```js
Sales Amount :=
SUMX ( Sales, Sales[Quantity] * Sales[Unit Price] )
```

_Quantity_ 和 _Unit Price_ 这两列在当前行上下文中各有一个值。在上一节中，我们展示了如果迭代中使用的表达式没有严格绑定到行上下文中，那么它将在筛选上下文中计算。这样的结果令人惊讶，至少对初学者来说是这样。不过，可以完全自由地在行上下文中使用任何函数。在众多可用的函数中，其中一个特殊的函数就是:_CALCULATE_。

如果在行上下文中执行，_CALCULATE_ 将在计算其表达式前先使行上下文失效。在 _CALCULATE_ 计算表达式时，所有之前的行上下文将不再有效。因此，下面的代码会产生语法错误:

```js
Sales Amount :=
SUMX ( 
  Sales, 
  CALCULATE ( Sales[Quantity] )    -- CALCULATE 里面没有行上下文，错误！
)
```

原因是不能在 _CALCULATE_ 中检索到 _Sales[Quantity]_列的值，因为 CALCULATE 会使其外部的行上下文失效。然而，这只是上下文转换执行的一部分。第二个，也是最相关的操作是，[[CALCULATE]] 将当前行上下文的所有列及其当前值添加为筛选参数。如下面的代码:

```js
Sales Amount :=
SUMX ( Sales, CALCULATE ( SUM ( Sales[Quantity] ) ) ) -- SUM 不需要行上下文
```

在 CALCULATE 中没有筛选参数。唯一的 CALCULATE 参数是要计算的表达式。因此，看起来 CALCULATE 没有覆盖现有的筛选上下文。重点是，由于上下文转换，CALCULATE 正在悄悄地创建许多筛选参数。它为被迭代表中的每一列创建一个筛选器。利用图 5-34 您可以对上下文转换有个初步的印象。为了直观的目的，我们使用了一组简化的列。

![[Pasted image 20220629222614.png]]

**图** **5-34** 当在行上下文中执行 _CALCULATE_ 时，它创建了一个筛选上下文，其筛选参数为当前被迭代表的所有列。

在迭代过程中，_CALCULATE_ 从第一行开始，并计算 S_UM (Sales[Quantity])_。即使没有筛选参数，_CALCULATE_ 也为迭代表的每一列添加一个筛选参数。也就是说，示例中有三列:_Product_、_Quantity_ 和 _Net Price_。因此，由上下文转换生成的筛选上下文包含每个列(_Product、Quantity、Net Price_)的当前值(A、1、11.00)。当然，在 SUMX 所做的迭代过程中，对于这三行中的每一行，这个过程都会继续下去。

换句话说，前一个 SUMX 的执行会引起如下三个 CALCULATE 函数的执行：

```js
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[Product] = "A",
    Sales[Quantity] = 1,
    Sales[Net Price] = 11
) +
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[Product] = "B",
    Sales[Quantity] = 2,
    Sales[Net Price] = 25
) +
CALCULATE (
    SUM ( Sales[Quantity] ),
    Sales[Product] = "A",
    Sales[Quantity] = 2,
    Sales[Net Price] = 10.99
)
```

这些筛选参数是隐藏的。它们由引擎自动添加，并且没有办法避免。一开始，上下文转换看起来很奇怪。然而，一旦习惯了上下文转换，它就是一个非常强大的特性。不那么容易掌握，但非常强大。

在进一步具体讨论之前，我们先总结一下前面的内容:

-   **上下文转换非常昂贵。**如果在一个包含 10 列和 100 万行的表的迭代过程中使用了上下文转换，那么 _CALCULATE_ 需要应用 10 个筛选器，100 万次。无论如何，这将是一个缓慢的操作。这并不是说应该避免依赖上下文转换。但它确实是 _CALCULATE_ 的一个需要谨慎使用的特性。
-   **上下文转换不只是筛选一行**。_CALCULATE_ 之外的原始行上下文始终只指向一行。行上下文逐行迭代。当通过上下文转换将行上下文移动到筛选上下文时，新创建的筛选上下文将筛选具有相同值集的所有行。因此，不可以假设上下文转换只创建包含一行的筛选上下文。这非常重要，我们将在下一节中回到这个主题。
-   **上下文转换包含所有列，包括公式中没有用到的列。**虽然筛选器中使用的列是隐藏的，但它们是表达式的一部分。这使得任何 _CALCULATE_ 公式都比看起来要复杂得多。如果使用上下文转换，那么表的所有列都作为隐藏的筛选参数成为表达式的一部分。这种行为可能会产生意想不到的依赖关系。本节稍后也将描述这个主题。
-   **上下文转换在行上下文之外创建筛选上下文。**您可能还记得计算上下文口诀，“行上下文迭代表，而筛选上下文筛选模型”。一旦上下文转换将行上下文转换为筛选上下文，它就会改变筛选器的性质。DAX 不再迭代单个行，而是筛选整个模型；关系成为等式的一部分。换句话说，发生在一个表上的上下文转换可能会将其筛选效果传播到远离行上下文起源表的地方。
-   **只要存在行上下文，就可以调用上下文转换。**例如，如果在计算列中使用 _CALCULATE_，就会发生上下文转换。在计算列中有一个自动的行上下文，这足以实现上下文转换。
-   **上下文转换转换所有行上下文。**当在多个表上执行嵌套迭代时，上下文转换将考虑所有行上下文。它使所有的行上下文无效，并将当前由所有活动行上下文迭代的所有列添加为筛选参数。
-   **上下文转换使行上下文无效。**虽然我们已经多次重复这个概念，但它值得再次引起您的注意。在通过 _CALCULATE_ 计算的表达式中，没有任何外部行上下文是有效的。所有外部行上下文都转换为等效的筛选上下文。

正如本节前面所预期的，这些考虑中的大多数需要进一步解释。在本节关于上下文转换的剩余部分中，我们将对这些要点进行更深入的分析。尽管所有这些注意事项都显示为警告，但实际上它们是重要的特性。对某些行为的忽视会导致意想不到的结果。然而，一旦您掌握了这种行为，您就会开始利用它，只要您觉得合适。一个奇怪的行为和一个有用的特性(至少在 DAX 中)之间的唯一区别是您的知识水平。

> [!NOTE] 上下文转换其实是自然而然的一个逻辑。并非强加进入 DAX 的一种机制。只有当你体会到这层自然性后，你才会充分释放该特性的能力。

## 计算列中的上下文转换

计算列在行上下文中求值。因此，在计算列中使用 _CALCULATE_ 将触发上下文转换。我们使用该特性在 _Product_ 表中创建一个计算列，该列将销售额占总销售额的 1% 以上的产品标记为“High Performance（高绩效）”。

生成这个计算列需要两个值：当前产品的销售额和所有产品的总销售额。前者需要筛选 _Sales_ 表，以便只计算当前产品的销售金额，而后者则需要在没有活动筛选器的情况下扫描 _Sales_ 表。代码如下:

```js
'Product'[Performance] =
VAR TotalSales =                               -- 所有产品的销售
    SUMX ( Sales,                              -- Sales 表没有被筛选
        Sales[Quantity] * Sales[Net Price] )   -- 所以计算的是总销售额
VAR CurrentSales =
    CALCULATE (                                  -- 执行上下文转换
        SUMX ( Sales,                            -- 仅是当前产品的销售
            Sales[Quantity] * Sales[Net Price] ) -- 这里我们仅计算
    )                                            -- 当前产品的销售额
VAR Ratio = 0.01                                 -- 1% 表示一个实数
VAR Result =
    IF (
        CurrentSales >= TotalSales * Ratio,
        "High Performance product",
        "Regular product"
    )
RETURN
    Result
```

可以看到这两个变量之间只有一个区别:_TotalSales_ 作为常规迭代执行，而 _CurrentSales_ 在一个 _CALCULATE_ 函数中执行相同的 DAX 代码。因为这是一个计算列，所以将行上下文转换为筛选上下文。筛选上下文通过模型传到 _Sales_ 表，只筛选当前产品的销售额。

因此，即使这两个变量看起来相似，它们的内容却完全不同。_TotalSales_ 计算所有产品的总销售额，因为计算列中的筛选上下文为空，不筛选任何内容。而 _CurrentSales_ 通过 _CALCULATE_ 执行的上下文转换只计算当前产品的销售额。

代码的其余部分是一个简单的 _IF_ 语句，用于检查是否满足条件并适当地标记产品。您可以在报告中使用该计算列，如图 5-35 所示。

![[Pasted image 20220629222637.png]]

**图** **5-35** 只有 4 个产品被标记为 High Performance

在 _Performance_ 计算列的代码中，我们使用了 _CALCULATE_ 和上下文转换特性。在继续之前，我们必须检查是否考虑了所有的可能情况。_Product_ 表很小，只包含几千行。因此，性能不是问题。由 _CALCULATE_ 生成的筛选上下文将筛选所有列。我们是否保证 _CurrentSales_ 只包含当前产品的销售？在这个例子中答案是肯定的。原因是 _Product_ 的每一行都是唯一的，因为 _Product_ 表每一行都有不同值——_Productkey_。因此，由上下文转换生成的筛选上下文保证只筛选一个产品。

在本例中，我们可以依赖上下文转换，因为被迭代的表的每一行都是唯一的。但这并不总是正确的。我们想用一个特意设计的例子来证明这一点。我们在 _Sales_ 中创建了一个计算列，其中包含以下代码:

```js
Sales[Wrong Amt] =
CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) )
```

作为一个计算列，它在行上下文中运行。_CALCULATE_ 执行上下文转换，因此在 _Sales_ 表中，_SUMX_ 会对与当前行列值相同的所有行进行迭代。问题是 _Sales_ 表没有任何具有唯一值的列。因此，有可能存在多个相同的行，如果它们存在，它们将一起筛选。换句话说，不能保证 _Wrong Amt_ 列中的 _SUMX_ 总是只迭代一行。

如果幸运的话，会有许多重复的行，并且由这个计算列计算的值是完全错误的。这样，问题就会很明显，并且马上就能被发现。在许多实际场景中，表中重复的行数很少，这些不准确的计算很难被发现并改正。本书中使用的示例数据库也不例外。图 5-36 显示了 _Sales Amount_ 的正确值，以及通过将 _Wrong Amt_ 计算列相加而计算出的错误值。

![[Pasted image 20220629222645.png]]

**图** **5-36** 大部分结果正确，只有两行具有不同的值

可以看到，两个度量值的差异只存在于汇总层面和 Fabrikam 品牌。在 _Sales_ 表中，有一些关于 Fabrikam 产品的行是重复的，继而被重复计算。这些重复行的存在是可能的：同样的客户在当天早上和下午在同一家商店购买相同的产品,但 _Sales_ 表只存储交易日期,而不是交易的时间。由于重复的数量很小,大多数数字看起来是正确的。然而,计算是错误的,因为它取决于表的内容。只要存在重复行就可能会导致结果不准确。并且重复行越多，结果就越不准确。

在这种情况下,依赖上下文转换是错误的选择。因为不能保证表中的任意一行是唯一的,这时使用上下文转换使用就不安全。DAX 编码专家应该提前知道这一点。此外, _Sales_ 表可能包含数百万行的行;因此,这个计算列不仅是错误的,也是非常缓慢的。

> [!NOTE] 上下文转换，会将所有的列全部转换这一特性是一个大坑，请死死地记住，在你使用 CALCULATE中 出错时，很有可能是这里出了问题，理解并可以自然地排除这个问题是晋级 DAX 专家的重要一步。

## 度量值中的上下文转换

由于 DAX 的另一个重要方面，理解上下文转换非常重要。

**每个度量值引用总是有伴随着一个隐式** _**CALCULATE**_**。**

在任何行上下文中，由于 _CALCULATE_ 的存在，度量值引用都将引起隐式上下文转换。这就是为什么在 DAX 中，在编写列引用(始终包含表名)和度量引用(始终不包含表名)时，使用正确的命名约定非常重要。这样您可以了解编写和读取 DAX 表达式的任何隐式上下文转换。

这个简单的初始定义值得用几个例子进行详细解释。首先，转换一个度量值引用总是需要先将度量值表达式封装在 _CALCULATE_ 函数中。例如，考虑 _Product_ 表中 _Sales Amount_ 度量值和 _Product Sales_ 计算列的定义:

```js
Sales Amount :=
SUMX ( Sales, Sales[Quantity] * Sales[Net Price] )

'Product'[Product Sales] =
[Sales Amount]
```

_Product Sales_ 计算列正确地计算了 _Product_ 表中仅含当前产品的 _Sales Amount_。确实，展开 _Product Sales_ 计算列定义中的 _Sales Amount_ 度量值，是包裹着 _Sales Amount_ 定义的 _CALCULATE_ 函数:

```js
'Product'[Product Sales] =
CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) )
```

如果没有 _CALCULATE_，计算列对所有产品的计算结果都是相同的值。该值对应 _Sales_ 表在没有任何在产品上的筛选时所有行的销售额。_CALCULATE_ 的存在意味着发生上下文转换，在本例中生成所需的结果。度量值引用总是调用 _CALCULATE_，这是非常重要的，可以用来编写简短而强大的 DAX 表达式，但是，如果您忘记了在行上下文中调用度量值时会发生上下文转换，就可能导致很大的错误。

一般来说，总是可以用封装在 _CALCULATE_ 中的度量值表达式替换度量值引用。考虑以下关于 _Max Daily Sales_ 的定义，它计算每日 _Sales Amount_ 的最大值:

```js
Max Daily Sales :=
MAXX ( 'Date', [Sales Amount] )
```

这个公式读起来很直观。但是，只筛选当天的销售额，并必须为每个日期计算 _Sales Amount_。这正是上下文转换所执行的操作。在内部计算时，DAX 用 _CALCULATE_ 封装公式替换了 _Sales Amount_ 度量值引用，如下例所示：

```js
Max Daily Sales :=
MAXX (
    'Date',
    CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) )
)
```

在第 7 章“使用迭代器和 _CALCULATE_”中，当我们开始编写复杂的 DAX 代码来解决特定场景时，我们将广泛地使用这个特性。这个初始描述只是完成了上下文转换的解释，在这些情况下会发生:

-   当在任何行上下文中调用 _CALCULATE_ 或 _CALCULATETABLE_ 函数时。
-   在任何行上下文中引用度量值时，因为度量值引用在 _CALCULATE_ 函数内部执行其 DAX 代码。

这种强大的行为可能会导致错误，主要在于不正确的假设，即可以用定义度量值的 DAX 代码替换该度量引用。不能这样假设。这在没有行上下文时可以工作，比如在度量值中，但在行上下文中引用度量值时就会有问题。这条规则很容易被忘记，因此我们通过一个例子来说明做出错误假设可能会发生什么。

在前面的示例中您已经看到，我们为计算列编写的代码，在 _Sales_ 上重复迭代了两次。以下是在前面的例子中已经给出的代码:

```js
'Product'[Performance] =
VAR TotalSales =                              -- 所有产品的销售
    SUMX ( Sales,                             -- Sales 表没有被筛选
        Sales[Quantity] * Sales[Net Price] )  -- 这里我们计算所有销售额
VAR CurrentSales =
    CALCULATE (                                  -- 执行上下文转换
        SUMX ( Sales,                            -- 仅是当前产品的销售
            Sales[Quantity] * Sales[Net Price] ) --这里我们仅计算
    )                                            -- 当前产品的销售额
VAR Ratio = 0.01                                 -- 1% 表示一个实数
VAR Result =
    IF (
        CurrentSales >= TotalSales * Ratio,
        "High Performance product",
        "Regular product"
    )
RETURN
    Result
```

_SUMX_ 对这两个变量执行的迭代代码是相同的:一个被 _CALCULATE_ 包围，而另一个没有。重写代码并使用度量值来托管迭代的代码似乎是一个好主意。如果表达式不是一个简单的 _SUMX_，而是一些更复杂的代码，您可能更愿意这么做。不幸的是，这种方法行不通，因为度量值引用将始终包含一个替换度量值的环绕 _CALCULATE_ 的表达式。

想象一下，创建一个 _Sales Amount_ 度量值，然后创建一个计算列，该列计算过程涉及两次调用度量值，其中一次使用 _CALCULATE_，一次不使用 _CALCULATE_。

```js
Sales Amount :=
SUMX ( Sales, Sales[Quantity] * Sales[Net Price] )

'Product'[Performance] =
VAR TotalSales = [Sales Amount]
VAR CurrentSales =
    CALCULATE ( [Sales Amount] )
VAR Ratio = 0.01
VAR Result =
    IF (
        CurrentSales >= TotalSales * Ratio,
        "High Performance product",
        "Regular product"
    )
RETURN
    Result
```

虽然看起来是个好主意，但这个计算列并没有计算预期的结果。原因是这两个度量值引用都有自己的隐式 _CALCULATE_。因此，总销售额并不计算所有产品的销售额。相反，它只计算当前产品的销售额，因为隐式 _CALCULATE_ 执行了上下文转换。_CurrentSales_ 计算了相同的值，它外面的 _CALCULATE_ 是多余的。实际上，因为引用了度量值，_CALCULATE_ 已经存在。这一点通过查看扩展 _Sales Amount_ 度量值所产生的代码更加明显:

```js
'Product'[Performance] =
VAR TotalSales =
    CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) )
VAR CurrentSales =
    CALCULATE ( CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) ) )
VAR Ratio = 0.01
VAR Result =
    IF (
        CurrentSales >= TotalSales * Ratio,
        "High Performance product",
        "Regular product"
    )
RETURN
    Result
```

任何时候在 DAX 中读取度量值调用时，您应该始终认为存在 _CALCULATE_。因为它就在那里。我们在第 2 章“介绍 DAX”中介绍了一个规则，其中我们说最好总是在列前面使用表名，而不要在度量前面使用表名。原因就在这里。

在阅读 DAX 代码时，最重要的是用户能够立即理解代码引用的是度量值还是列。事实上，几乎每个 DAX 程序员都采用的标准是在度量值前省略表名。

自动 _CALCULATE_ 使得编写使用迭代执行复杂计算的公式变得很容易。当在第七章开始编写复杂的 DAX 代码来解决特定的场景时，我们将广泛使用这个特性。

## 其他

[[CALCULATE中的行上下文转换和筛选器参数]]

[[上下文转换和扩展表]]

[[ALLSELECTED和上下文转换]]

[[阴影筛选上下文]]