---
created: 2022-05-16
tags:
 - dax圣经
 - 数据沿袭
 - 高级原理
 - dax进阶
subject: dax圣经
importance: 5
skilled: 3
status: 
url: 
cover: 
author: sqlbi
---

数据沿袭的基本规则：
1.  数据模型中表的每一列都有唯一的数据沿袭。
2.  当筛选上下文筛选模型时，它筛选的是与筛选上下文中的列具有相同数据沿袭的模型列。
3.  由于筛选器的本质是表，了解表函数如何影响数据沿袭很重要：
	-   通常，用于分组数据的列在结果中保留其数据沿袭 。
	-   包含聚合结果的列总是具有新的数据沿袭 。
	-   由 [[ROW]]和 [[ADDCOLUMNS]]创建的列总是产生一个新的数据沿袭 。
	-   由 [[SELECTEDCOLUMNS]]创建的列在表达式只是数据模型中的列的副本时，保留原始列的数据沿袭；否则，它们将产生一个新的数据沿袭 。

例如，下面的代码看起来产生一个表，其中每个产品颜色具有相应的 Sales Amount值，用于对该颜色的所有销售进行求和 。相反，因为 C2是由 [[ADDCOLUMNS]]创建的列，所以它不具有与 Product[Color] 相同的沿袭，即使它具有相同的内容 。请注意，我们必须使用几个步骤：
- 首先，我们必须创建 C2 列；
- 然后我们只选择该列 。
- 如果其他列保留在相同的表中，将得到另一个结果 。

```js
DEFINE
    MEASURE Sales[Sales Amount] =
        SUMX (
            Sales,
            Sales[Quantity] * Sales[Net Price]
        )
EVALUATE
VAR NonBlueColors =
    FILTER (
        ALL ( 'Product'[Color] ),
        'Product'[Color] <> "Blue"
    )
VAR AddC2 =
    ADDCOLUMNS (
        NonBlueColors,
        "[C2]", 'Product'[Color]
    )
VAR SelectOnlyC2 =
    SELECTCOLUMNS (
        AddC2,
        "C2", [C2]
    )
VAR Result =
    ADDCOLUMNS (
        SelectOnlyC2,
        "Sales Amount", [Sales Amount]
    )
RETURN
    Result
ORDER BY [C2]
```

先前的查询产生一个结果，其中 _Sales Amount_ 列总是具有相同的值，对应于销售表中的所有行的总和 。

![](https://s2.loli.net/2022/05/16/ay91itzfsdD7hCk.png)


[[TREATAS]]可用于转换表的数据沿袭 。例如，以下代码将数据沿袭还原为 _Product [Color]_，以便最后一个 [[ADDCOLUMNS]]利用 _Color_ 列上的上下文转换计算 _Sales Amount_ ：

```js
DEFINE
    MEASURE Sales[Sales Amount] =
        SUMX (
            Sales,
            Sales[Quantity] * Sales[Net Price]
        )
EVALUATE
VAR NonBlueColors =
    FILTER (
        ALL ( 'Product'[Color] ),
        'Product'[Color] <> "Blue"
    )
VAR AddC2 =
    ADDCOLUMNS (
        NonBlueColors,
        "[C2]", 'Product'[Color]
    )
VAR SelectOnlyC2 =
    SELECTCOLUMNS (
        AddC2,
        "C2", [C2]
    )
VAR TreatAsColor =
    TREATAS (
        SelectOnlyC2,
        'Product'[Color]
    )
VAR Result =
    ADDCOLUMNS (
        TreatAsColor,
        "Sales Amount", [Sales Amount]
    )
RETURN
    Result
ORDER BY 'Product'[Color]
```

副作用是，[[TREATAS]]更改了列名，必须在ORDER BY 条件中使用正确的列名 。
![](https://s2.loli.net/2022/05/16/ay91itzfsdD7hCk.png)


## 扩展阅读
[[理解数据沿袭]]
