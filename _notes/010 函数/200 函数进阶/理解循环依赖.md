---
created: 2022-05-17
tags: 循环依赖
subject: dax进阶 
importance: 5
skilled: 3
status:
author: sqlbi
url: https://www.sqlbi.com/articles/understanding-circular-dependencies/
cover: 
---


```js
Line Amount = Sales[Quantity] * Sales[Net Price]
Line Cost = Sales[Quantity] * Sales[Unit Cost]
Line Margin = Sales[Line Amount] - Sales[Line Cost]
```

这个例子很简单，Line Margin依赖前面两列，所以它的计算一定是放在这两列后面才行。从上面的例子也可以很明显的看出，如果修改了Line Amount或者Line Cost，则Line Margin也会跟着发生变化 。

但是，在某些情况下DAX引擎不知道先计算哪一个，比如A依赖B，B又依赖A，这就变成发先有鸡还是先有蛋的问题了，DAX无法计算A和B的。

通常在以下情况下会遇到循环依赖

-   使用[[CALCULATE]]创建计算列时
-   尝试在两个表间创建关系时：一个表是计算表，或关系是基于计算列

## 计算列中的循环依赖

为了演示循环依赖，创建了如下计算表，它在模型中并没有直接和其他表创建关系。

```js
BestProducts =
CALCULATETABLE (
    VAR MinSale = PERCENTILEX.INC ( 'Product', [Sales Amount], 0.95 )
    VAR Result =
        SELECTCOLUMNS (
            FILTER (
                ALL ( 'Product' ),
                [Sales Amount] >= MinSale
            ),
            "ProductKey", 'Product'[ProductKey],
            "Product Name", 'Product'[Product Name]
        )
    RETURN Result,
    'Date'[Calendar Year] = "CY 2008"
)
```

然后添加两个计算列

```js
Test1 = CALCULATE ( COUNTROWS ( 'BestProducts' ), BestProducts[Product Name] >= "Contoso" )
Test2 = CALCULATE ( COUNTROWS ( 'BestProducts' ), BestProducts[Product Name] >= "Contoso" )
```

虽然两个计算列完全相同，但是在创建第二个计算列时我们遇到的循环依赖的错误提示
![](https://s2.loli.net/2022/05/21/YpPNM1vQlqX4T9y.png)


虽然上述代码的筛选表达式只使用了[Product Name]列，但是需要注意的是表达式使用了[[CALCULATE]]，并且计算列是在行上下文中评估的。

当[[CALCULATE]]在行上下文中运行时，它会执行上下文转换，上下文转换会在当前迭代的所有列上添加一个筛选器

```js
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084,
    BestProducts[Product Name] = "Contoso SLR Camera X143 Grey"
)
```

```js
Test1 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084,
    BestProducts[Product Name] = "Contoso SLR Camera X143 Grey",
    BestProducts[Test2] = 1
)
 
Test2 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084,
    BestProducts[Product Name] = "Contoso SLR Camera X143 Grey",
    BestProducts[Test1] = 1
)
```

外部的[[CALCULATE]]执行上下文转换之后才执行内部的筛选

继续看下面的代码

```js
Test1 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084,
    BestProducts[Product Name] = "Contoso SLR Camera X143 Grey",
    BestProducts[Test2] = 1
)
 
Test2 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084,
    BestProducts[Product Name] = "Contoso SLR Camera X143 Grey",
    BestProducts[Test1] = 1
)
```

很明显上面的代码产生了循环依赖，要想解决通常有两种方案

-   修改循环依赖的关系
-   依赖特定的dax代码优化

比如，我们可以使用ALLEXCEPT来改变上下文，从上下文转换中删除不需要的列

```js
Test2 =
CALCULATE (
    COUNTROWS ( 'BestProducts' ),
    BestProducts[Product Name] >= "Contoso",
    ALLEXCEPT ( BestProducts, BestProducts[ProductKey] )
)
```

继续看下面的代码

```js
Test1 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084
)
 
Test2 =
CALCULATE (
    CALCULATE (
        COUNTROWS ( 'BestProducts' ),
        BestProducts[Product Name] >= "Contoso"
    ),
    BestProducts[ProductKey] = 1084
)
```

循环依赖不再存在了，

我们还可以使用另一种方法，通过在Sales表和BestProducts表之间创建关系，也可以避免循环依赖，因为两表间存在关系，DAX引擎会强制验证ProductKey是否是BestProducts表中具有唯一值的列。如果表中存在唯一值列，则优化上下文转换为依赖该列，而不筛选其他列。

但是，以下情况并不适用 列代码中使用了[[USERELATIONSHIP]]，可能会遇到包含主键表的循环依赖 在SSAS 或PowerBI数据集中创建了计算列

所以，建议使用[[ALLEXCEPT]]来优化循环依赖，因为它适用于任何情况

## 关系中的循环依赖

假设我们已经创建了下面的关系

![](https://s2.loli.net/2022/05/21/YpPNM1vQlqX4T9y.png)

上面的关系可以正常工作，但是当我们想把关系修改为Prduct：BestProducts = 1:N时就会报错

![](https://s2.loli.net/2022/05/21/SDzfeJt4vZVKcOW.png)


造成这样的原因并不是因为循环依赖，而是因为两个表间只要存在关系就会有产生依赖关系。

之所以产生这样的结果的一个重要原因是空值，[[处理DAX中的空白]]

看下面的代码

```js
BestProducts =
CALCULATETABLE (
    VAR MinSale = PERCENTILEX.INC ( 'Product', [Sales Amount], 0.95 )
    VAR Result =
        SELECTCOLUMNS (
            FILTER (
                ALL ( 'Product' ),
                [Sales Amount] >= MinSale
            ),
            "ProductKey", 'Product'[ProductKey],
            "Product Name", 'Product'[Product Name]
        )
    RETURN Result,
    'Date'[Calendar Year] = "CY 2008"
)
```

我们能想到的第一个优化是使用[[ALLNOBLANKROW]]代替[[ALL]]

另外，上面的代码实际上等效于下面，筛选条件实际上筛选的是表

```js
BestProducts =
CALCULATETABLE (
    VAR MinSale = PERCENTILEX.INC ( 'Product', [Sales Amount], 0.95 )
    VAR Result =
        SELECTCOLUMNS (
            FILTER (
                ALL ( 'Product' ),
                [Sales Amount] >= MinSale
            ),
            "ProductKey", 'Product'[ProductKey],
            "Product Name", 'Product'[Product Name]
        )
    RETURN Result,
    FILTER (
        ALL ( 'Date'[Calendar Year] ),
        'Date'[Calendar Year] = "CY 2008"
)
```

最终替换为如下

```js
BestProducts =
CALCULATETABLE (
    VAR MinSale = PERCENTILEX.INC ( 'Product', [Sales Amount], 0.95 )
    VAR Result =
        SELECTCOLUMNS (
            FILTER (
                ALLNOBLANKROW ( 'Product' ),
                [Sales Amount] >= MinSale
            ),
            "ProductKey", 'Product'[ProductKey],
            "Product Name", 'Product'[Product Name]
        )
    RETURN Result,
    FILTER (
        ALLNOBLANKROW ( 'Date'[Calendar Year] ),
        'Date'[Calendar Year] = "CY 2008"
)
```

这个版本的代码消除了循环依赖的问题

再看下面的例子

![](https://s2.loli.net/2022/05/21/SDzfeJt4vZVKcOW.png)

要创建Product和Price Range之间的关系，需要创建一个包含PriceRangeKey的计算列

```js
PriceRangeKey =
VAR CurrentPrice = 'Product'[Unit Price]
VAR Result =
    CALCULATE (
        VALUES ( 'Price Ranges'[PriceRangeKey] ),
        'Price Ranges'[Min Value] <= CurrentPrice,
        'Price Ranges'[Max Value] > CurrentPrice
    )
RETURN
    Result
```

计算列可以正常创建，但是当我们想创建关系时，就会出现错误提示，原因同样是可能会存在空值，所以使用[[DISTINCT]]代替[[VALUES]]即可

```js
PriceRangeKey =
VAR CurrentPrice = 'Product'[Unit Price]
VAR Result =
    CALCULATE (
        DISTINCT ( 'Price Ranges'[PriceRangeKey] ),
        'Price Ranges'[Min Value] <= CurrentPrice,
        'Price Ranges'[Max Value] > CurrentPrice
    )
RETURN
    Result
```

## 结论

循环依赖有时确实很难被发现，但其实也不难，比如在使用[[ALL]]和[[VALUES]]时就很容易发生循环依赖。
