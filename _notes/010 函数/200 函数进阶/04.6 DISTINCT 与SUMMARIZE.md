---
created: 2022-06-26
tags:
 - dax圣经
 - 上下文
subject: dax圣经
importance: 5
skilled: 4
status: 
author: sqlbi
url: 
cover: 
---


要求计算 Contoso 客户的平均年龄。即使这看起来是一个合理的要求，但这描述不完整。我们是在问这些客户的此时年龄还是他们在购买那刻的年龄？如果一个客户购买了 3 次，那算均值年龄时计算一次还是计算三次？如果他们在不同的年龄购买过一共 3 次呢？我们需要更加精确的描述。现在这是更加完整的要求："计算客户在交易发生时的平均年龄，如果客户在同一年龄购买过多次，则仅记录一次记录。“

该解决方案可以分为两步：

-   计算销售发生时的客户年龄
-   计算均值

每次销售时客户年龄都会发生变化，因此年龄需要储存在 Sales 表中，对于 Sales 表的每一行能计算出在销售发生时客户的年龄。一条计算列正适合这个需求。

```js
Sales[Customer Age] =
-- 计算销售订单发生日于客户生日之间的年份差
  DATEDIFF ( 
    RELATED ( Customer[Birth Date] ),
    Sales[Order Date], 
    YEAR 
)  
```

因为客户年龄是一个计算列，它在迭代表的行下文中进行计算。该公式需要 _Customer_ 表中的 _Birth Date_ 列，_Customer_ 表在 _Sales_ 表关系的一端。在这个例子中，需要使用 _RELATED_ 函数是 DAX 代码能够传递到目标表中。在 Contoso 这个样本数据集中，有许多 _Birth Date_ 为空的客户。如果第一个参数为空，则 _DATEDIFF_ 返回空值。因为需求是要计算均值，首先第一个不正确的结果可能是这个度量值：

```js
Avg Customer Age Wrong := AVERAGE ( Sales[Customer Age] )  
```

这个结果是不正确的，因为如果一个客户在一个年龄时购买了多笔，则 _Sales_ 表的 Customer Age 中一个顾客会有多个相同的年龄行。这个需求是要每个客户只计算一次，该公式并非提供这样的需求，Figure4-25 图中最后一行显示了预期的结果：

![[Pasted image 20220626231020.png]]

**图 4-25** 一个简单均值运算对于客户年龄是错误的

现在有个问题，每个客户的年龄应该只计算一次。一个可行的解决方法是对客户年龄使用 [[DISTINCT]] 函数然后求均值—实际也是错的，其度量值如下：

```js
Avg Customer Age Wrong Distinct :=
--对 Sales 表中 Customer Age 的不重复值迭代计算出其均值。
AVERAGEX (
  DISTINCT ( Sales[Customer Age] )，
  Sales[Customer Age] 
 )
```

该结果目前还不是正确的，事实上，DISTINCT 函数返回的是客户年龄的不同值，两个客户有相同年龄的话这个公式只会统计一次。而要求是每个客户计算一次年龄，而不是每个年龄计算一次。事实上，图 4-26 显示新公式 Avg Customer Age Wrong Distinct 的结果，你看到仍旧错误的。

![[Pasted image 20220626231026.png]]

**图 4-26** 不同客户年龄的均值仍是错误的结果

在最后这个公式中，尝试将 _CustomerKey_ 替换掉 _Customer Age_ 作为 _DISTINCT_ 函数的参数。

```js
Avg Customer Age Invalid Syntax :=
--对 Sales 表中 CustomerKey 的不重复值迭代并客户年龄的均值。
AVERAGEX ( 
  DISTINCT ( Sales[CustomerKey] ), 
  Sales[Customer Age] 
)  
```

该代码显示错误 DAX 并不会执行它，你能在不看下文前对其做出解释吗？

[[AVERAGEX]] 函数产生一个行上下文用来迭代单张表。该表提供给 AVERAGEX 函数的第一个函数是 _DISTINCT_ ( _Sales[CustomerKey]_ ),_DISTINCT_ 函数返回一张仅包含一列是 _CustomerKey_ 去重后的结果的表。因为 AVERAGEX 产生的行上下文只有 _Sales[CustomerKey]_的一列。DAX 无法计算在仅包含 Sales[CustomerKey]的列中使用行上下文计算 Sales[Customer Age]。

现在需要一个行上下文既要包含 _Sales[CustomerKey]_ 的粒度，又要包含 _Sales[Customer Age]_。第三章中介绍的 *SUMMARIZE 函数将会产生两列各值唯一组合的表。现在我们能为所有需求展示完整的代码了：

```js
Correct Average :=
-- 将 Sales 表中 CustomerKey 和 CustomerAge 所有结果组合起来进行迭代进而计算客户年龄的平均值
AVERAGEX ( 
  SUMMARIZE ( 
    Sales, 
    Sales[CustomerKey], 
    Sales[Customer Age] 
  ), 
  Sales[Customer Age] 
)  
```

通常，对于多步计算可以使用变量来区分。但在本案例的场景，请注意使用 _CustomerAge_ 列时仍要在 [[AVERAGEX]] 函数的第二个参数中引用 _Sales_ 表名（而不可以是：CustomersAge[Customer Age]，其中 CustomersAge 是变量，如下所示）。一个变量可以是一个表，但变量不能是作为数据模型表被引用。

```js
Correct Average :=
VAR CustomersAge =
  SUMMARIZE ( 
    Sales, 
    Sales[CustomerKey], 
    Sales[Customer Age] 
)
RETURN
--对 Sales 表中 Customer Age 列迭代并计算 Customer Age 的平均值
AVERAGEX ( 
  CustomersAge, 
  Sales[Customer Age] 
)  
```

> 注意 这里有两种写法，第一种：

```js
AVERAGEX ( 
CustomersAge, 
Sales[Customer Age] 
)  
```

> 这里的 CustomersAge 是变量，它已经表示了一个表，这个表是匿名的，注意，这个表 CustomersAge 本身不能作为表引用，可以作为表引用的表必须是预先在数据模型种存在的表。而表 CustomersAge 的构成，由于数据沿袭，Sales[Customer Age] 恰好是其中的列，所以可以这样使用。关于数据沿袭，会在后续章节介绍。

> 第二种：

```js
AVERAGEX ( 
CustomersAge, 
[Customer Age] 
)  
```

> 由于 CustomerAge 已经变成匿名表，我们可以直接使用其中的列而不带有表引用，其语法含义为在当前表中的列。随着对 DAX 使用的深入，大家可以采用这种方式快速地编写 DAX 公式，相当流畅。

> 为了进一步理解 VAR 带来的匿名表与数据模型表的区别，请仔细考察：

```js
VAR X = { 1 , 2 } 可以的
VAR X = Order  可以的

对于 VAR X = Order

SUMX( X , X[Amount] ) 不可以
SUMX( Order , Order[Amount] ) 可以
SUMX( X , [Amount] ) 可以
SUMX( X , Order[Amount] ) 可以
SUMX( Order , [Amount] ) 可以

其中

Order 是表引用
X 不是表引用
```

SUMMARIZE 对当前筛选上下文中已知的客户和年龄产生所有组合。因此相同年龄的不同客户会重复年龄，而单一客户仅一个年龄。AVERAGEX 忽略表中 _CustomerKey_ 的存在，它只使用 _Customer Age_ 列，_CustomerKey_ 仅用来计算每个年龄的正确出现次数。

我们需要强调在报告中生成的完整的度量值是如何被筛选上下文执行的。于是，只有剩那些已经购买过并且被 [[SUMMARIZE]] 返回的客户。报告中每个单元格都有不同的筛选上下文，现在仅考察那些至少购买过一次并且产品颜色显示在报告中的客户。