---
created: 2022-06-26
tags:
 - dax圣经
 - 表函数
 - all家族
subject: dax圣经
importance: 5
skilled: 4
status: 
author: sqlbi
url: 
cover: 
---

有时我们想扩展行的数量，以便进行某种计算。在这种情况下，DAX 提供了一组为此目的而设计的函数：[[ALL]], [[ALLEXCEPT]], [[ALLCROSSFILTERED]], [[ALLNOBLANKROW]]和 [[ALLSELECTED]]。在

根据使用的参数，**ALL 返回表的所有行或一列或多列的所有值**。例如，以下 DAX 表达式返回一个 _ProductCopy_ 计算表，它是包含 _Product_ 表中所有行的副本：

```js
ProductCopy = ALL ( 'Product' )
```

> **注** ALL 在计算表中不需要，因为没有筛选器能影响它。然而，ALL 在度量值中是很有用的，如下面的例子所示。

当我们需要计算百分比时，ALL 非常有用，因为它能够忽略报表中自动引入的过滤器。假设我们需要一个如图下所示的报表，在同一行显示销售金额和给定金额占总金额的百分比。

![[Pasted image 20220626210101.png]]

_Sales Amount_

```js
Sales Amount := 
SUMX ( 
    Sales, 
    Sales[Quantity] * Sales[Net Price] 
)
```

为了计算百分比，我们把各销售额除以总数。因此，即使当报表在筛选一个既定类别时，公式也必须计算总销售额。这可以通过使用 _ALL_ 函数来获得。事实上，无论在报告中使用什么过滤，以下度量值都会计算所有销售额的总和：

```js
All Sales Amount := 
SUMX ( 
    ALL ( Sales ), 
    Sales[Quantity] * Sales[Net Price]
)
```

在公式中，我们用 _ALL（sales）替换了对 sales_ 的引用，充分利用了 _ALL_ 函数。此时，我们可以通过一个简单的除法来计算百分比：

```js
Sales Pct := DIVIDE ( [Sales Amount], [All Sales Amount] )
```

![[Pasted image 20220626210146.png]]

**ALL_的参数不能是表表达式**。它需要是表名或列名列表。那么如果我们改用列，结果会怎样？在这种情况下，_ALL_ 返回整个表中列的所有不同值。类别计算表从产品表的“类别”列中获取：

```js
Categories = ALL ( 'Product'[Category] )
```

![[Pasted image 20220626210158.png]]

我们可以在 _ALL_ 函数的参数中指定同一表中的多个列。在这种情况下，_ALL_ 返回这些列中所有现有值的排列组合。例如，我们可以通过在 _ALL_ _的参数_值列表中添加 Product[Subcategory] 列来获得所有类别和子类别排列组合的列表

```js
Categories = 
ALL ( 
    'Product'[Category], 
    'Product'[Subcategory] 
)
```

![[Pasted image 20220626210208.png]]

-   ALL 会忽略任何现有的过滤器。我们可以将 ALL 用作迭代函数（如 [[SUMX]]和 [[FILTER]]）的参数，或者用作 [[CALCULATE]] 函数中的 [[FILTER]] 参数。
-   如果我们想在 ALL 函数中调用表的大多数列，但不是所有列，那么可以使用 [[ALLEXCEPT]]。ALLEXCEPT 的语法要求表后跟要排除的列。因此，ALLEXCEPT 返回一个表，其中包含表中其他列中现有值组合的唯一列表。
-   ALLEXCEPT 自动在结果中包含将来可能出现在表中的任何其他列。例如，如果我们有一个包含五列（_ProductKey_, _Product Name_, _Brand_, _Class_, _Color_）的产品表，则以下两个表达式将产生相同的结果：

```js
ALL ( 'Product'[Product Name], 'Product'[Brand], 'Product'[Class] ) 
ALLEXCEPT ( 'Product', 'Product'[ProductKey], 'Product'[Color] )
```

但是，如果我们稍后要增加 Product[Unit Cost]和 Product[Unit Price]，ALL 的结果将忽略它们，而 ALLEXCEPT 将返回对等的值：

```js
ALL ( 
    'Product'[Product Name], 
    'Product'[Brand], 
    'Product'[Class], 
    'Product'[Unit Cost], 
    'Product'[Unit Price] 
)
```

换言之，使用 **ALL 声明我们想要的列，而使用 ALLEXCEPT 声明我们想要从结果中移除的列**。
**ALLEXCEPT 主要用作高级计算中的一个 CALCULATE 参数**，它很少与简单的公式一起使用。因此，我们把它的描述放在这里是为了完整性，它只有在以后的学习过程中才会变得有用。

> **一级类别和子类别** 列举一个 _ALL_ 作为表函数的例子，假设我们想要生成一个仪表板，显示销售金额超过平均销售金额两倍的产品的类别和子类别。要生成此报表，我们需要首先计算每个子类别的平均销售额，然后在确定值后，从子类别列表中检索销售额大于平均值两倍的子类别。 下面的代码生成了该表，值得深入研究以了解表函数和变量的功能：

```js
BestCategories =
VAR Subcategories =
    ALL (
        'Product'[Category],
        'Product'[Subcategory]
    )
VAR AverageSales =
    AVERAGEX (
        Subcategories,
        SUMX (
            RELATEDTABLE ( Sales ),
            Sales[Quantity] * Sales[Net Price]
        )
    )
VAR TopCategories =
    FILTER (
        Subcategories,
        VAR SalesOfCategory =
            SUMX (
                RELATEDTABLE ( Sales ),
                Sales[Quantity] * Sales[Net Price]
            )
        RETURN
            SalesOfCategory >= AverageSales * 2
    )
RETURN
    TopCategories
```

> 第一个变量（_Subcategories）存储所有类别和子类别的列表。然后，AverageSales 计算每个子类别的平均销售额。最后，TopCategories_ 从子类别中删除销售金额不超过平均销售价值两倍的子类别。

![[Pasted image 20220626210220.png]]