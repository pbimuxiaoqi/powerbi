---
created: 2022-06-26
tags:
 - dax圣经
 - 表函数
subject: dax圣经
importance: 5
skilled: 4
status: 
author: sqlbi
url: 
cover: 
---

ALL 始终返回列的所有不同值。另一方面，[[VALUES]] 只返回不同的可见值。通过观察以下两个度量值，您可以了解这两种方式之间的差异：

```js
NumOfAllColors := COUNTROWS ( ALL ( 'Product'[Color] ) ) 
NumOfColors := COUNTROWS ( VALUES ( 'Product'[Color] ) )
```

_NumOfAllColors_ 统计产品表的所有颜色，而 _NumOfColors_ 只统计报表中给定筛选器的颜色。

![[Pasted image 20220626211044.png]]

因为报表是按类别划分的，所以每个既定的类别都包含一些颜色的产品，而不是全部颜色的产品。VALUES 返回在当前筛选器中计算的列的不同值。如果在计算列或计算表中使用 VALUES 或 [[DISTINCT]]，则它们的结果与 ALL 相同，因为没有[[行上下文]]。另一方面，当在度量值中使用时，这两个函数会考虑到现有的上下文来计算它们的结果，而 ALL 会忽略任何筛选器。

> 这是初学者常犯的一个错误，他以为计算列也在被筛选，其实计算列的计算先于任何计算而存在，因此在计算列或计算表中使用 ALL 和 VALUES 的效果是一样的。

如前面所述，这两个函数几乎相同。现在重要的是理解为什么 VALUES 和 DISTINCT 是同一行为的两种变体。不同之处在于它们考虑表中是否存在空行。首先，我们需要了解如果没有显式创建空行，空行是如何出现在表中的。

事实上，以防关系无效，引擎会自动在关系一侧的任何表中创建一个空行。为了演示这种行为，我们从产品表中删除了所有的银色产品。最初有 16 种不同的颜色，而我们去掉了一种颜色，所以我们认为颜色总数是 15 种。然而，_NuMofallColors_ 仍然是 16，并且报告在顶部显示了一个没有名称的新行。

![[Pasted image 20220626211052.png]]

因为 _Product_ 表是 _Sales_ 表关系的一方，所以对于 _Sales_ 表中的每一行，_Product_ 表中都有一个相关行。尽管如此，因为我们故意删除了具有一种颜色的所有产品，所以现在 _Sales_ 中有许多行与 _Product_ 表不再具有有效关系。请注意，我们没有从 _Sales_ 表中删除任何行；我们删除了一个颜色意图破坏这种关系。

为了确保在所有计算中都考虑这些行，引擎会自动向产品表中添加一行，该行的所有列中都是空白。_Sales_ 表中的所有孤立行都链接到这个新引入的空白行。

尽管 _Sales_ 表中引用的多个不同产品在 _Product_ 表中不再具有相应的 _ProductKey_，但仅在 Product*表添加一个空行。

你可以看到第一行显示的类别为空白，并且只有一种颜色。该数字来自一个空白行，其中类别是空白、颜色是空白，表中所有列中都是空白。如果检查表是看不到该行的，因为它是在加载数据模型期间自动创建的行。如果在某个时候，如果您又重新添加银色产品，则该关系再次生效，空白行将从表中消失。

DAX 中的某些函数将空白行视为其结果的一部分，而另一些则不考虑。具体来说，_VALUES_ 将空行视为有效行，并返回它。另一方面，_DISTINCT_ 不返回空白行。您可以通过查看以下新度量值来了解差异，该度量值使用 DISTINCT 而非 VALUES 来计算不同颜色的个数：

```js
NumOfDistinctColors := COUNTROWS ( DISTINCT ( 'Product'[Color] ) )
```


![[Pasted image 20220626211100.png]]

一个设计良好的模型不应该出现任何无效的关系。因此，如果您的模型是完美的，那么这两个函数总是返回相同的值。不过，在处理无效关系时，您需要注意这种行为，否则可能会导致编写错误的计算。例如，假设我们要计算每个产品的平均销售额。一个可行的解决方案是使用以下代码计算总销售额并除以产品数量：

```js
AvgSalesPerProduct := 
DIVIDE ( 
  SUMX ( 
    Sales, 
    Sales[Quantity] * Sales[Net Price] 
  ), 
  COUNTROWS ( 
    VALUES ( 'Product'[Product Code] ) 
  ) 
)
```

![[Pasted image 20220626211110.png]]

第一行中显示的数字（其中 _Category_ 为空）对应于所有银产品的销售情况，而这些银产品不再存在于 _Product_ 表中。这一空白行关联所有银色的产品，而这些产品不再存在于产品表中。除法的分子考虑所有银产品的销售额。除法的分母为由 VALUES 返回的单个空行。因此，单个不存在的产品（空白行）累积了销售表中引用的许多其他产品的销售额，而在产品表中没有可用的产品，从而导致了巨大的数值。在这里，问题出在无效的关系，而不是公式本身。事实上，无论我们创建什么公式，在 _Sales_ 表中都有许多产品的销售额，而数据库对此没有任何信息。然而，研究同一计算的不同公式如何返回不同的结果是很有用的。考虑这两个其他变化：

```js
AvgSalesPerDistinctProduct := 
DIVIDE ( 
  SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ), 
  COUNTROWS ( DISTINCT ( 'Product'[Product Code] ) ) 
) 

AvgSalesPerDistinctKey := 
DIVIDE ( 
  SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ), 
  COUNTROWS ( VALUES ( Sales[ProductKey] ) ) 
)
```

在第一个变体中，我们使用 DISTINCT 而不是 VALUES。结果，[[COUNTROWS]]返回一个空值，结果将为空。在第二个变体中，我们仍然使用 VALUES，但这次我们计算的是 _Sales[productkey]_。请记住，有许多不同的 Sales[productkey]*值都与同一空行相关。

![[Pasted image 20220626211119.png]]

有趣的是，_AvgSalesPerDistinctKey_ 是唯一正确的计算。由于我们按类别进行切分，每个类别都有不同数量的无效产品，所有这些都累积到一个空白行中。值得注意的是： **无效的产品，指的是订单中对应不到的产品；而不是产品表中的产品**。

> 假设一个模型本来 所有产品都有订单，同时所有订单的产品又都存在，这是理想状态。而原作者为了说明 _VALUES_ 和 _DISTINCT_ 的区别，故意将某种颜色的产品从产品表中删除。这就导致有的订单数据是对应不到产品的。由于 颜色和类别 是多对多的，而这些被删除的产品散落在不同产品大类。对应不到产品的订单数据，与什么产品对应呢？答案是：空白行。因此，_VALUES( 产品[ID] )_ 会比 _DISTINCT( 产品[ID] )_ 大 1。用 _VALUES( 产品[ID] )_ 做分母如果想正确就需要满足两个前提：

-   所有的订单中涉及的产品在产品表中都有对应产品
-   所有产品表的产品都产生了订单

> 基本不会同时满足上述两个条件，因此使用订单表的产品 ID 是最准确的：_VALUES( 订单[产品 ID] )_ 是正确的分母。

**在模型中不要有任何无效的关系**。如果由于某些原因，你有无效的关系，那么你需要非常谨慎地处理空白行，以及它的存在如何影响你的计算。

最后请注意，如果存在，ALL 函数总是返回空白行。如果您需要从结果中移除空白行，那么 [[ALLNOBLANKROW]] 是您将要使用的函数。

> **多列 VALUES** VALUES 和 DISTINCT 只接受单个列作为参数。对于两列或更多列没有对应的版本，对于 ALL 和 ALLNOBLANKROW 也是如此。

```js
VALUES ( 'Product'[Category], 'Product'[Subcategory] )  
```

> 可以被替换为：

```js
SUMMARIZE ( 'Product', 'Product'[Category], 'Product'[Subcategory] ) 
```

> _VALUES_ 和 _DISTINCT_ 可以支持表作为参数，但 _VALUES_ 只能接受表引用，而 _DISTINCT_ 可以接受表引用或表表达式。

稍后，您将看到 VALUES 和 DISTINCT 经常用作迭代函数的参数。只要关系有效，它们的结果就没有区别。在这种情况下，当迭代一列的值时，需要将空白行视为有效行，以确保迭代所有可能的值。根据经验，VALUES 应该是您的默认选择，只有当您希望显式排除可能的空白值时，才使用 DISTINCT

VALUES 和 DISTINCT 也接受表作为参数。在这种情况下，他们表现出不同的行为：

-   DISTINCT 是返回表的不同行，而不考虑空白行。并从结果中删除重复的行。
-   VALUES 返回表中的所有行，不删除重复项，如果存在空白行，则加上，且重复的行保持不变。