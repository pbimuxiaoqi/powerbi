---
created: 2022-06-26
tags:
 - 表函数
 - dax圣经
subject: dax圣经
importance: 5
skilled: 4
status: 
author: sqlbi
url: 
cover: 
---

[[FILTER]]，语法如下：

```js
FILTER ( <table>, <condition> )
```

FILTER 有两个参数，分别是一个表和一个逻辑条件。最终，FILTER 会返回满足条件的所有行。FILTER 既是一个[[表函数]]同时也是一个[[迭代器]]。为了返回结果，它**逐行扫描计算条件的表**。换句话说，就是迭代这张表。

例如，下面的计算表返回 Fabrikam 产品

```js
FabrikamProducts =
FILTER (
    'Product',
    'Product'[Brand] = "Fabrikam"
)
```

FILTER 通常用于在**迭代中减少行数**。例如，如果开发人员想要计算红色产品的销售额，他们可以编写如下度量：

```js
RedSales :=
SUMX (
    FILTER (
        Sales,
        RELATED ( 'Product'[Color] ) = "Red"
    ),
    Sales[Quantity] * Sales[Net Price]
)
```

你可以在图 3-2 中看到结果，以及总销售额。

![[Pasted image 20220626205433.png]]

RedSales 度量值是遍历 sales 表与红色产品相关的一个子集。**FILTER 是在现有条件下添加条件**

例如，RedSales 在 Audio 中是指既是音频类别和又是红色产品的销售额。

可以在一个 FILTER 函数中嵌套另一个 FILTER。一般来说，嵌套两个 FILTER 函数产生的结果与将两个 FILTER 函数条件与一个 AND 函数组合在一起的结果相同。换句话说，以下两个查询产生相同的结果：

```js
FabrikamHighMarginProducts =
FILTER (
    FILTER (
        'Product',
        'Product'[Brand] = "Fabrikam"
    ),
    'Product'[Unit Price] > 'Product'[Unit Cost] * 3
)
```

```js
FabrikamHighMarginProducts =
FILTER (
    'Product',
    AND (
        'Product'[Brand] = "Fabrikam",
        'Product'[Unit Price] > 'Product'[Unit Cost] * 3
    )
)
```

但是，根据条件的选择性，两段代码的性能在大型表中可能会有所不同。**如果一个条件比另一个条件更具选择性，则首先使用嵌套的 **FILTER** 函数应用最多选择条件被认为是最佳实践。

例如，如果 Fabrikam 品牌有许多产品，但很少有定价为其成本三倍的产品，则下述查询将过滤应用于最里面的 FILTER 中的单价和单位成本。通过这样做，公式首先应用最严格的过滤，以减少检查品牌所需的迭代次数：

```js
FabrikamHighMarginProducts =
FILTER (
    FILTER (
        'Product',
        'Product'[Unit Price] > 'Product'[Unit Cost] * 3
     ),
    'Product'[Brand] = "Fabrikam"
)
```

使用 FILTER，开发人员通常可以编写易于阅读和维护的代码，例如，假设需要计算红色产品的数量。如果不使用表函数：

```js
NumOfRedProducts :=
SUMX (
    'Product',
    IF ( 'Product'[Color] = "Red", 1, 0 )
)
```

内部 IF 函数根据产品的颜色返回 1 或 0，然后将此表达式求和返回红色产品的数量。更好的执行措施如下：

```js
NumOfRedProducts :=
COUNTROWS (
    FILTER ( 'Product', 'Product'[Color] = "Red" )
)
```

后一个表达式更好地显示了开发人员想要获得的内容。此外，这个代码不仅对人来说更容易阅读，而且 DAX 优化器也能更好地理解开发人员的意图。因此，优化器生成一个更好的查询计划，进而产生更好的性能。