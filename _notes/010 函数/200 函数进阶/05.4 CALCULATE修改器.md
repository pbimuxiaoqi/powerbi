---
created: 2022-07-02
tags:
 - calculate
 - dax圣经
subject: dax圣经
importance: 5
skilled: 3
status: 
author: sqlbi
url: 
cover: 
---


[[CALCULATE]]接受几个不同的修改器，这些修改器会更改和形成新筛选上下文。
- [[ALL]]。当在CALCULATE筛选参数中直接使用ALL时，它充当CALCULATE修改器，而不是表函数。
- 其他重要的修改器包括[[USERELATIONSHIP]]、[[CROSSFILTER]]和[[ALLSELECTED]]，它们有单独的描述。[[ALLEXCEPT]]、[[ALLSELECTED]]、[[ALLCROSSFILTERED]]和[[ALLNOBLANKROW]]修改器具有与ALL相同的优先级规则。


## 理解 USERELATIONSHIP

第一个 CALCULATE 修改器是 USERELATIONSHIP。在计算表达式时，_CALCULATE_ 可以使用此修改器激活关系。数据模型可能同时包含活动关系和非活动关系。两个表之间可以有多个关系，但只能有一个关系是活动的，而其余的关系就是非活动的。

例如，可以在 _Sales_ 表中存储每个订单的订单日期（_Order Date_）和交付日期（_Delivery Date）。通常需要基于订单日期执行销售分析，但可能某些特定度量值需要考虑交付日期。在这种情况中，一个选项是在 Sales_ 和 _Date_ 之间创建两个关系:一个基于订单日期（_Order Date_），另一个基于交付日期（_Delivery Date_）。模型如图 5-37 所示。

![](http://files.excel120.com/images/20191203195536.png)

**图** **5-37** _Sales_ 和 _Date_ 通过两个关系连接，但只能有一个关系是活动的

一次只能激活这两个关系中的一个。例如，在这个演示模型中，与订单日期（_Order Date_）的关系是活动的，而与交付日期（_Delivery Date_）链接的关系是不活动的。要编写一个度量值来显示给定时间段内的交付额，需要在计算期间激活与交付日期的关系。在这种情况下，USERELATIONSHIP 非常有用，如下面的代码所示:

```js
Delivered Amount :=
CALCULATE (
    [Sales Amount],
    USERELATIONSHIP ( Sales[Delivery Date], 'Date'[Date] )
)
```

在计算 Sales _Amount_ 时 _Delivery Date_ 与 _Date_ 的关系被激活。同时，与 _Order Date_ 的关系被停用。请记住，在给定的时间点上，任何两个表之间只能激活一个关系。因此，USERELATIONSHIP 暂时激活了一个关系，而停用了 _CALCULATE_ 之外活动的关系。

图 5-38 显示了基于 _Order Date_ 的 _Sales Amount_ 与新的 _Delivered Amount_ 度量值之间的差异。

![](http://files.excel120.com/images/20191203195637.png)

**图** **5-38** 该图说明了订购和交付销售之间的差异

当使用 USERELATIONSHIP 激活关系时，您需要注意一个重要方面：关系是在使用表引用时定义的，而不是在调用 _RELATED_ 或其他关系函数时定义的。我们将在第 14 章中通过使用扩展表来详细讨论。在本章，理解这个例子就足够了。若要计算所有在 2007 年实现的销售额（以发货日期为准），以下公式将报错：

```js
Delivered Amount 2007 v1 :=
CALCULATE (
    [Sales Amount],
    FILTER (
        Sales,
        CALCULATE (
            RELATED ( 'Date'[Calendar Year] ),
            USERELATIONSHIP ( Sales[Delivery Date], 'Date'[Date] )
        ) = "CY 2007"
    )
)
```

事实上，内层 _CALCULATE_ 会将 _FILTER_ 迭代生成的行上下文转换成筛选上下文，导致在其内部使用 RELATED 函数报错，因为这个函数必须在行上下文中使用。因此，在 _CALCULATE_ 表达式中，根本不能使用 _RELATED_ 函数（上面代码第 7 行）。可以编写下面的代码：

```js
Delivered Amount 2007 v2 :=
CALCULATE (
    [Sales Amount],
    CALCULATETABLE (
        FILTER ( Sales, RELATED ( 'Date'[Calendar Year] ) = "CY 2007" ),
        USERELATIONSHIP ( Sales[Delivery Date], 'Date'[Date] )
    )
)
```

在这个公式中，_Sales_ 是在 CALCULATETABLE*（该原则也适用于 CALCULATE）激活所需关系后引用的。因此，在 *FILTER 内部使用 _RELATED_ 时，与 _Delivery Date_（交货日期）的关系处于活动状态。_Delivered Amount 2007 v2_ 度量值可以工作，但更好的公式是依赖于默认的筛选上下文传播，而不是依赖于 _RELATED_:

```js
Delivered Amount 2007 v3 :=
CALCULATE (
    [Sales Amount],
    'Date'[Calendar Year] = "CY 2007",
    USERELATIONSHIP ( Sales[Delivery Date], 'Date'[Date] )
)
```

在 _CALCULATE_ 语句中使用 _USERELATIONSHIP_ 时，所有的筛选参数都使用同一 CALCULATE 函数中出现的关系修改器计算，而无关它们与 _USERELATIONSHIP_ 的先后顺序如何。例如，在 _Delivered Amount 2007 v3_ 度量值中，_USERELATIONSHIP_ 修改器影响谓词筛选 _Calendar Year_（日历年），尽管筛选参数出现在关系修改器之前。

这种行为使得在计算列表达式中使用非默认关系成为一个复杂操作。表的调用在计算列定义中是隐式的。因此，您不能控制它，并且不能利用 _CALCULATE_ 和 _USERELATIONSHIP_ 来更改该行为。

一个重要的注意事项是：_USERELATIONSHIP_ 本身并不引入任何筛选器。实际上，_USERELATIONSHIP_ 不是一个筛选参数。它是 _CALCULATE_ 的一个修改器，只改变其他筛选器应用于模型的方式。如果您仔细查看 _Delivered Amount in 2007 v3_ 的定义，可能会注意到筛选参数在 2007 年应用了一个筛选器，但是它没有指出要使用哪个关系。是使用 _Order Date_ 还是 _Delivery Date要使用的关系由 USERELATIONSHIP_ 定义。

因此，_CALCULATE_ 首先通过激活关系来修改模型的结构，然后才应用筛选参数。如果不是这样，也就是说，如果筛选参数总是根据当前关系体系结构计算，将无法计算。

在筛选参数和 _CALCULATE_ 修改器的应用中有优先规则。第一个规则是 _CALCULATE_ 修改器总是应用于任何筛选参数之前，因此筛选参数作用于修改后的模型。稍后我们将更详细地讨论 _CALCULATE_ 参数的优先级。
-   Delivered Amount 是对的，没有什么可说的。
-   Delivered Amount 2007 v1 是错的，从原理上就错了，由于上下文转换，RELATED 不能用于 CALCUALTE 第一个参数。
-   Delivered Amount 2007 v2 没有原理错误，但其计算是不能实现计算按发货日期计算在 2007 年的销售额的，原因在于：它返回的 Sales 是一个扩展表，包括了 Sales 的所有列；当 CALCULATETABLE 执行完毕，回到 CALCULATE，关系也就恢复成了订单日期。因此，此时的筛选将计算订单日期与发货日期在2007年且订单日期与发货日期对应相同（在同一天）的订单。因此，该度量值无法实现预期计算逻辑，是错的，隐藏的很深的错误。
-   Delivered Amount 2007 v2.1 该版本由 PowerBI 战友联盟会员提问，将 Delivered Amount 2007 v2 变形为：

```js
Delivered Amount 2007 v2.1 :=
CALCULATE (
    [Sales Amount],
    FILTER ( Sales, RELATED ( 'Date'[Calendar Year] ) = "CY 2007" ),
    USERELATIONSHIP ( Sales[Delivery Date], 'Date'[Date] )
)
```

该度量值计算结果与 Delivered Amount 2007 v2 完全相同，而且语义也相同，其原因在于：
1. 首先计算：USERELATIONSHIP ( Sales\[Delivery Date\], 'Date'\[Date\] )，将关系修改为发货日期；
2. 其次计算：FILTER ( Sales, RELATED ( 'Date'\[Calendar Year\] ) = "CY 2007" )，但在计算该内容时却使用了未修改关系之前的状态，也就是订单日期；由于返回了 Sales，仍然是扩展表；
3. 在第 1 步关系修改为发货日期后，联立第 2 步得到的全部以订单日期为准的 Sales 扩展表，这将筛选出订单日期与发货日期在2007年且订单日期与发货日期对应相同（在同一天）的订单。因此，该度量值无法实现预期计算逻辑，是错的，隐藏的很深的错误。

-   值得注意的是，Delivered Amount 2007 v2 与 Delivered Amount 2007 v2.1 很类似但又有不同：
    -   这两个版本的错误都隐藏得非常深；
    -   这两个版本的错误都与扩展表有关；
    -   不同的是 v2 是按发货日期的扩展表与订单日期再求交集，而 v2.1 是按订单日期的扩展表与发货日期再求交集；
    -   v2.1 版本的错误更加隐蔽而复杂，它首先更改了关系，而在 FILTER 计算时却使用了原关系，计算完成后又由于扩展表与修改了的关系进行交集。
-   Delivered Amount 2007 v3 是简单且可以完成预期目标的写法。
-   这个案例便是 DAX 复杂性的一个诠释，尤其是 v2.1 版本的复杂性值得注意。在现实环境中，应该尽量避免同时使用多个 CALCULATE 不同类型的参数，而应该使用嵌套以体现明确的逻辑。

## 理解 CROSSFILTER

 [[CROSSFILTER]] 有点类似于 [[USERELATIONSHIP]]，它操作模型中关系的体系结构。然而，CROSSFILTER可以执行两种不同的操作：
-   可以改变关系的交叉筛选方向。
-   它会使一段关系失效。

_USERELATIONSHIP_ 允许您在禁用活动关系的同时激活另一个非活动的关系，但如果不激活非活动的关系，则不能禁用相同表之间的活动关系。_CROSSFILTER_ 的工作方式不同。_CROSSFILTER_ 接受两个参数，这两个参数是关系中涉及的列，第三个参数可以是 _NONE_, _ONEWAY_,或 _BOTH。例如，下面的度量值是在激活 Sales_ 和 Product 之间的双向关系后，计算产品不重复颜色的数量:

```js
NumOfColors :=
CALCULATE (
    DISTINCTCOUNT ( 'Product'[Color] ),
    CROSSFILTER ( Sales[ProductKey], 'Product'[ProductKey], BOTH )
)
```

与 _USERELATIONSHIP_ 一样，_CROSSFILTER_ 本身并不引入筛选器。它只改变关系的结构，将应用筛选器的任务留给其他筛选参数。在前面的示例中，只有关系的效果影响 _D__**I**__STINCTCOUNT_ 函数，因为 _CALCULATE_ 没有其它的筛选参数。

## 理解 KEEPFILTERS

在本章前面将 KEEPFILTERS 作为一个 _CALCULATE_ 修改器作了介绍。严格意义上，_KEEPFILTERS_ 不是 _CALCULATE_ 的修改器，而是一个筛选参数的修改器。实际上，它并不改变 _CALCULATE_ 的整体计算。相反，它改变了将某个筛选参数应用于 _CALCULATE_ 生成的最终筛选上下文的方式。

我们已经深入讨论了 _CALCULATE_ 的行为，如下面的计算:

```js
Contoso Sales :=
CALCULATE ( [Sales Amount], KEEPFILTERS ( 'Product'[Brand] = "Contoso" ) )
```

_KEEPFILTERS_ 的存在意味着 _Brand_ _上_的筛选器不会覆盖同一列上已经存在的筛选器。相反，新筛选器被添加到筛选上下文中，而已存在的筛选器保持不变。_KEEPFILTERS_ 应用于使用它的单个筛选参数，不改变整个 _CALCULATE_ 函数的语义。

还有另一种使用 _KEEPFILTERS_ 的方法，这种方法不太明显。可以使用 _KEEPFILTERS_ 作为被迭代表的修改器，如下面的代码所示:

```js
ColorBrandSales :=
SUMX (
    KEEPFILTERS ( ALL ( 'Product'[Color], 'Product'[Brand] ) ),
    [Sales Amount]
)
```

KEEPFILTERS 作为迭代中使用的顶级函数的存在，迫使 DAX 对在上下文转换期间 _CALCULATE_ 添加的隐式筛选参数使用 _KEEPFILTERS_。事实上，在对 Product[Color]和 Product[Brand]_的值进行迭代时，SUMX_ 调用 _CALCULATE_ 作为对 _Sales Amount_ 度量值计算的一部分。此时，将发生上下文转换，行上下文转化为筛选上下文，_Color_ 和 _Brand_ 添加为筛选参数。

因为迭代是从 _KEEPFILTERS_ 开始的，所以上下文转换不会覆盖现有的筛选器。它将与现有的筛选器求交集。在迭代中使用 _KEEPFILTERS_ 作为顶级函数是不常见的。我们将在第 10 章后面介绍这种高级用法的一些例子。

更详细的见什么时候使用Keepfilters

## 理解 CALCULATE 中的 ALL

正如在第 3 章中所学到的，_ALL_ 是一个表函数。不过，当在 _CALCULATE_ 中用作筛选参数时，ALL 充当 _CALCULATE_ 修改器。虽然函数名相同，但作为 _CALCULATE_ 修改器的语义与预期略有不同。

查看以下代码，您可能会认为 _ALL_ 返回所有年份，并且它更改了筛选上下文，使所有年份都可见:

```js
All Years Sales :=
CALCULATE ( [Sales Amount], ALL ( 'Date'[Year] ) )
```

然而，事实并非如此。当在 _CALCULATE_ 的筛选参数中用作顶级函数时，_ALL_ 将移除现有的筛选器，而不是创建新的筛选器。更适合 _ALL_ 的名字应该是 _REMOVEFILTER_。由于历史原因，这个名称保留了下来，但最好准确地知道该函数的行为方式。

如果将 _ALL_ 都看作一个表函数，_CALCULATE_ 行为将如图 5-39 所示。

![](http://files.excel120.com/images/20191203200606.png)

**图** **5-39** 看起来 _ALL_ 返回所有年份，并使用列表覆盖前面的筛选上下文

最内层 _Date[Year]上的 ALL_ 是 _CALCULATE_ 中的一个顶级 _ALL_ 函数调用。因此，它并不表现为一个表函数。它应该被读作 REMOVEFILTERS。实际上，在这种情况下，_ALL_ 不是返回所有年份，而是作为一个 _CALCULATE_ 修改器，从其参数中删除任何筛选器。在 _CALCULATE_ 内部实际发生的是图 5-40 中的关系图。

![](http://files.excel120.com/images/20191203200648.png)

**图** **5-40** 当用作 _REMOVEFILTER_ 时，_ALL_ 删除了上下文中的原有筛选器

这两种行为之间的区别很微妙。在大多数计算中，这种语义上的细微差别会被忽略。然而当我们开始编写更高级的代码时，这个小的差异将产生很大的影响。目前，重要的细节是，当 _ALL_ 用作 _REMOVEFILTER_ 时，它充当一个 _CALCULATE_ 修改器，而不是表函数。

**这一点很重要，因为在 _****CALCULATE****_ 中筛选器有不同的优先顺序。** _CALCULATE_ 修改器在显式筛选参数之前被用于最终筛选上下文。因此，考虑一个列上存在 _ALL_，_KEEPFILTERS_ 被用于该列上的另一个显式筛选器；它产生的结果与没有 _KEEPFILTERS_ 应用于相同列的筛选器相同。换句话说，下面两个 _Sales Red_ 度量值结果相同:

```js
Sales Red :=
CALCULATE ( [Sales Amount], 'Product'[Color] = "Red" )

Sales Red :=
CALCULATE (
    [Sales Amount],
    KEEPFILTERS ( 'Product'[Color] = "Red" ),
    ALL ( 'Product'[Color] )
)
```

原因是 _ALL_ 是一个 _CALCULATE_ 修改器。因此，_ALL_ 应用于 _KEEPFILTERS_ 之前。此外，其他以 _ALL_ 为前缀的函数具有相同的优先规则：这些函数是 _ALL、ALLSELECTED、ALLNOBLANKROW、ALLCROSSFILTERED_ 和 *ALLEXCEPT。我们通常将这些函数称为 _ALL_ 函数。作为一个规则，当在 _CALCULATE_ 筛选参数中用作顶级函数时，_ALL_ 函数都是 _CALCULATE_ 修改器。

## 没有参数的 ALL 和 ALLSELECTED

我们在第三章中介绍了 *ALLSELECTED。我们很早就介绍了它，主要是因为它非常有用。与所有 _ALL_ 函数一样，_ALLSELECTED_ 在 _CALCULATE_ 中用作顶级函数时充当 _CALCULATE_ 修改器。此外，在介绍 _ALLSELECTED_ 时，我们将其描述为一个表函数，它可以返回列或表的值。

下面的代码计算当前可视区域所选颜色占所有颜色的百分比。原因是 ALLSELECTED 将恢复 _Product[Color]_列上当前可视内容之外的筛选上下文。

```js
SalesPct :=
DIVIDE ( [Sales], CALCULATE ( [Sales], ALLSELECTED ( 'Product'[Color] ) ) )
```

使用 _ALLSELECTED (Product)_ 可以得到类似的结果，它在整个表上执行 _ALLSELECTED_。不过，当用作 CALCULATE* 修改器时，_ALL_ 和 _ALLSELECTED_ 也可以在没有任何参数的情况下工作。

因此，下面是一个有效的语法:

```js
SalesPct :=
DIVIDE ( [Sales], CALCULATE ( [Sales], ALLSELECTED () ) )
```

您可以很容易地注意到，在这种情况下，_ALLSELECTED_ 不能是一个表函数。它是一个 _CALCULATE_ 修改器，指示 _CALCULATE_ 恢复当前可视内容之外活动的筛选上下文。整个计算过程相当复杂。我们将在第 14 章中进一步探讨 _ALLSELECTED_ 的行为。类似地，没有参数的 _ALL_ 从模型中的所有表中清除筛选器上下文，恢复一个没有活动筛选器的筛选上下文。

现在我们已经完成了 _CALCULATE_ 的总体结构，我们终于可以详细讨论涉及 _CALCULATE_ 的所有元素的求值顺序。