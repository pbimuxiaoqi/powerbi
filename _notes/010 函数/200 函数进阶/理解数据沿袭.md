---
created: 2022-05-16
tags:
 - 数据沿袭
 - 高级原理
subject: dax进阶
importance: 5
skilled: 3
status: 
url: "https://www.sqlbi.com/articles/understanding-data-lineage-in-dax/"
cover: 
author: sqlbi
---

## 数据沿袭简介
在 DAX 中，数据沿袭是一个标记（Tag）。表的每一列都会分配这样一个标记，它的作用是标识数据模型中的原始列。我们已经知道表函数可以对模型中的一列或多列进行操作，当操作对象对应于数据模型的一个物理列时，因为每列都具有特定的沿袭，即使经过某些转换和加工，引擎仍然可以识别这种沿袭关系，从而可以更快地进行筛选操作。沿袭不取决于列的名称，也不取决列的值，从技术上讲，它是可以唯一标识列的内部标准。你无法通过 DAX 显示列的沿袭，但可以观察这种效果。  
  
>数据沿袭是一个隐式生效的 DAX 特性，设计的十分巧妙，以至于大多数用户可以在不了解它的情况下使用它  

比如
```js
EVALUATE VALUES( 'Product'[Category] )
```
[[VALUES]]返回的结果包含8行数据
![](https://s2.loli.net/2022/05/16/9MFkVdBLGf4wXxv.png)
然而这8行数据并不仅仅是8个字符串，dax知道这些字符串来源于Product[Category]列，作为Product表中的一列，它继承了筛选器通过关系传递后筛选模型中其他表的能力。这也是为什么存在一个上下文转换迭代 [[VALUES]] ( Product[Category] ) 来筛选Sales表
考虑下面的代码
```js
EVALUATE
ADDCOLUMNS(
    VALUES( 'Product'[Category] ),
    "Amt",[Sales Amount]

)
```
结果包含每个产品类别的销售额
![](https://s2.loli.net/2022/05/16/YQZjeEqwRd8o19O.png)
## 数据沿袭不取决于列值 
再看下面的代码
```js
EVALUATE
VAR Categories =
    DATATABLE (
        "Category", STRING,
        {
            { "Category" },
            { "Audio" },
            { "TV and Video" },
            { "Computers" },
            { "Cameras and camcorders" },
            { "Cell phones" },
            { "Music, Movies and Audio Books" },
            { "Games and Toys" },
            { "Home Appliances" }
        }
    )
RETURN
    ADDCOLUMNS ( Categories, "Amt", [Sales Amount] )
```

![](https://s2.loli.net/2022/05/16/CDo7HYqflBeTtA5.png)
所有类别的结果值都一样，虽然列名和值都一样，但是它是不具备数据沿袭的，
对于想要筛选模型的值，DAX 需要验证值本身的数据沿袭。一个容易理解的知识是，<font color="red">数据模型中的列值具备该列的数据沿袭。与之相对的，如果一个值没有链接到数据模型中的任何列，那么它就是一个匿名值。</font>
## 数据沿袭不取决于列名  
对列的重命名不会破坏原有的沿袭。例如，下面的查询为每一行返回不同的值:  
```js
EVALUATE
ADDCOLUMNS (
    SELECTCOLUMNS (
        VALUES ( 'Product'[Category] ),
        "New name for Category", 'Product'[Category]
    ),
    "Amt", [Sales Amount]
)
```
尽管 New name for Category 列与原始列名不同，但它保留了 Product[Category]的数据沿袭，所以查询得到了正确的结果，按类别划分的销售额。  
  ![](https://s2.loli.net/2022/05/16/1R8PSY7mXoVlsy6.png)

  
>对于能否筛选模型，列名和列值都不重要，真正重要的只是列的数据沿袭，也就是这些值的源头所在的列  

即使一个表包含了来自不同表的列，每列也仍然保持自己的数据沿袭。正因如此，<font color="red">表表达式的结果可以一次将筛选器应用于多个表。</font>通过下面的查询你可以清楚的观察到这一现象，查询同时包含 Product[Category]和 Date[Calendar Year]列，这两列都通过上下文转换产生的筛选上下文将其筛选器应用于 Sales Amount 度量值。  
  
```js
EVALUATE  
FILTER (  
	ADDCOLUMNS (  
		CROSSJOIN (  
		VALUES ( 'Product'[Category] ),  
		VALUES ( 'Date'[Calendar Year] )  
		),  
		"Amt", [Sales Amount]  
	),  
	[Amt] > 0  
) 
``` 
  
结果显示了给定类别和年份的销售额，表表达式的两列都有效的筛选了度量值  
![](https://s2.loli.net/2022/05/16/rcywtnRCb8ELivG.png)

  
## 改变数据沿袭  
### 修改列表达式失去沿袭  
当表达式只有一个列引用时，会保持数据沿袭，而一旦加入其他表达式，情况就可能发生变化。例如，向之前表达式中的 Product[Category]添加空字符串不会改变列值，但是会破坏数据沿袭。在下面的代码中，New name for Category 的源头变成了一个表达式，不再是列引用。因此，这个新列具备了一个新的数据沿袭，与模型的任何列都不相关。  
  
```js
EVALUATE  
ADDCOLUMNS (  
SELECTCOLUMNS (  
VALUES ( 'Product'[Category] ),  
"New name for Category", 'Product'[Category] & ""  
),  
"Amt", [Sales Amount]  
)  
```
  
![](https://s2.loli.net/2022/05/16/yEYDJutrRgcQNwM.png)

### 使用 TREATAS 改变沿袭  
数据沿袭由引擎以完全自动的方式继承和维护，但你仍然可以修改表的数据沿袭，这就是 [[TREATAS]] 函数的用处。[[TREATAS]] 接受表作为第一参数，后跟一列或多列的引用列表，返回表继承参数列的数据沿袭。并且对于第一参数的每一列，[[TREATAS]] 剔除其在各自的输出列中不存在的值。例如，下面的查询构建了一个包含产品类别列表的表，其中高亮行的值“Computers and Geeky Stuff”与模型中的任何类别都不匹配。我们使用 [[TREATAS]] 强制将表的数据沿袭映射到 Product[Category]。  
  
```js
EVALUATE  
VAR Categories =  
DATATABLE (  
"Category", STRING,  
{  
{ "Category" },  
{ "Audio" },  
{ "TV and Video" },  
{ "Computers and geeky stuff" },  
{ "Cameras and camcorders" },  
{ "Cell phones" },  
{ "Music, Movies and Audio Books" },  
{ "Games and Toys" },  
{ "Home Appliances" }  
}  
)  
RETURN  
ADDCOLUMNS (  
TREATAS (  
Categories,  
'Product'[Category]  
),  
"Amt", [Sales Amount]  
)  
```
结果返回按类别划分的销售额，但没有“Computers and Geeky Stuff”的记录，因为模型的原始数据中没有这个类别，数据沿袭改变后，TREATAS 从结果中删除了这一行。  
  ![](https://s2.loli.net/2022/05/16/RfM8YPTjxiKGUFV.png)

## 操作数据沿袭
计算每个产品的首销日的销售额
一个可能的方案是逐个产品迭代销售表，找到最早的销售日期，然后计算该日期的销售额，看下面的代码
```js
FirstDaySales v1 :=
SUMX (
    'Product',
    VAR FirstSale =
        CALCULATE (
            MIN ( Sales[Order Date] )
        )
    RETURN
        CALCULATE (
            [Sales Amount],
            'Date'[Date] = FirstSale
        )
)
```
  
![](https://s2.loli.net/2022/05/16/hCMHkysrAx3Twt2.png)

虽然结果是正确的，但是性能却不是最优的，它迭代Product表，为每个产品生成一个上下文转换，又在Date表应用筛选条件，而不是利用关系。我们来修改下代码，我们修改的核心是<font color="red">减少迭代</font>
- 构建一个包含产品名称和包含其首销日期的表
- 然后基于该表对Sales表进行筛选

```js
FirstDaySales v2 :=
VAR ProductsWithSales =
    SUMMARIZE (
        Sales,
        'Product'[Product Name]
    )
VAR ProductsAndFirstDate =
    ADDCOLUMNS (
        ProductsWithSales,
        "Date First Sale", CALCULATE (
            MIN ( Sales[Order Date] )
        )
    )
VAR Result =
    SUMX (
        ProductsAndFirstDate,
        VAR DateFirstSale = [Date First Sale]
        RETURN CALCULATE (
            [Sales Amount],
            'Date'[Date] = DateFirstSale
        )
    )
RETURN Result
```

仔细观察上面这个查询，你可能会注意到，变量 ProductsAndFirstDate包含了产品名称和首销日期，如果直接将它用作 CALCULATE 的筛选器参数，似乎是一种更简洁的写法

```js
FirstDaySales v3 (wrong) :=
VAR ProductsWithSales =
    SUMMARIZE (
        Sales,
        'Product'[Product Name]
    )
VAR ProductsAndFirstDate =
    ADDCOLUMNS (
        ProductsWithSales,
        "Date First Sale", CALCULATE (
            MIN ( Sales[Order Date] )
        )
    )
VAR Result =
    CALCULATE (
        [Sales Amount],
        ProductsAndFirstDate
    )
RETURN Result
```
很遗憾，这种写法是错误的，公式没有应用任何筛选器，返回与 Sales Amount 相同的值，问题的原因就在于数据沿袭
实际上， ProductsAndFirstDate 中 [[ADDCOLUMNS]] 的结果包含一个产品和一个日期；但从数据沿袭的角度来看，产品名称属于 Product[Product Name]的原生列，而 [Date First Sale ]没有Date表的数据沿袭，这是 [[MIN]] 表达式的结果。 First Sale 列有自己的数据沿袭，与数据模型中的其他表无关。
所以，修改上面的代码也很简单，通过[[TREATAS]]强制修改[Date First Sale ]和Date[Date]的关系即可

```js
FirstDaySales v4 :=
VAR ProductsWithSales =
    SUMMARIZE (
        Sales,
        'Product'[Product Name]
    )
VAR ProductsAndFirstDate =
    ADDCOLUMNS (
        ProductsWithSales,
        "First Sale", CALCULATE (
            MIN ( Sales[Order Date] )
        )
    )
VAR ProductsAndFirstDateWithCorrectLineage =
    TREATAS (
        ProductsAndFirstDate,
        'Product'[Product Name],
        'Date'[Date]
    )
VAR Result =
    CALCULATE (
        [Sales Amount],
        ProductsAndFirstDateWithCorrectLineage
    )
RETURN Result
```


## 结论
对于高级 DAX 用户，理解并灵活运用数据沿袭是一项重要的技能。它不像[[行上下文]][[筛选上下文]]和[[上下文转换]]那样基础。但它是将你和其他普通 DAX 用户区分开的重要概念之一。

## 附件
[理解数据沿袭](obsidian://open?vault=BI&file=9999%20%E9%99%84%E4%BB%B6%2FUnderstanding%20data%20lineage%20in%20DAX.pbix)